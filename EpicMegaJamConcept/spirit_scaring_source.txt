title EpicJamGame
author Toombler
homepage www.puzzlescript.net

again_interval 0.05
realtime_interval 0.1

run_rules_on_level_start

(verbose_logging)

========
OBJECTS
========

Background
#CCDCEA #99C6E2  #ffaf47 #ffbf69
11011
01110
11001
11100
00111

WaterCoversDone
transparent


WaterCoverUp
#7fb0d7 #4089c4 (#96bede #67a1d0) (#95e6de #2ec2b4)
00000
11111
.....
.....
.....

WaterCoverDown
transparent #2c757d
.....
.....
.....
.....
00000

WaterCoverRight
transparent #2c757d
....0
....0
....0
....0
....0

WaterCoverLeft
transparent #2c757d
0....
0....
0....
0....
0....

WaterCoverUpRightCorner
transparent #2c757d
....0
.....
.....
.....
.....

WaterCoverDownRightCorner
transparent #2c757d
.....
.....
.....
.....
....0

WaterCoverUpLeftCorner
transparent #2c757d
0....
.....
.....
.....
.....

WaterCoverDownLeftCorner
transparent #2c757d
.....
.....
.....
.....
0....


SandIsU
transparent

SandIsD
transparent

SandIsR
transparent

SandIsL
transparent



Target
DarkBlue
.....
.000.
.0.0.
.000.
.....

Wall
#3a3c3a #505451 #6a6e6b #8c908c
20203
22302
02312
03010
20010


(BROWN DARKBROWN
00010
11111
01000
11111
00010)



Player
#383430 #A09D95 #6D6C69 #574236
.000.
.111.
33333
02220
.0.0.

(
Player
Red green darkblue darkgray
.000.
.111.
02220
02220
.0.0.)

StoneCrate
#a05f75 #a35c68 #b14e6d #94555c #da737b #6d4b78
02322
3...2
3...2
3...1
33331

StoneCrateSink0
#966179 #995E6E #A65172 #8B5863
02322
3...2
3...2
3...1
33331

StoneCrateSink1
#8C637E #8F6073 #9A5577 #835A69
02322
3...2
3...2
3...1
33331

StoneCrateSink2
#836482 #856279 #8F587D #7A5D70
02322
3...2
3...2
3...1
33331

StoneCrateSink3
#796687 #7B647F #845B82 #716077
02322
3...2
3...2
3...1
33331

StoneCrateSink4
#6F688B #716684 #785F87 #68637E
02322
3...2
3...2
3...1
33331

StoneCrateSink5
#656A90 #66698A #6D628C #606584
02322
3...2
3...2
3...1
33331

StoneCrateSink6
#5B6C94 #5C6B8F #616691 #57688B
02322
3...2
3...2
3...1
33331

StoneCrateSink7
#516E99 #526D95 #566996 #4E6B92
02322
3...2
3...2
3...1
33331

StoneCrateSink8
#486F9D #486F9B #4B6C9C #456E99
02322
3...2
3...2
3...1
33331

StoneCrateSink9
#3E71A2 #3E71A0 #3F70A1 #3D709F
02322
3...2
3...2
3...1
33331

(
#bb5044 #9f443a #c33c51 #9a2d32 #da737b #6d4b78
02322
3...2
3...2
3...1
33331)



Spirit
Purple
.....
.000.
.000.
.000.
.....

(glass is solid but not opaque)

GlassWall
lightblue
(0.1.0
00.1.
.00.1
1.00.
.1.00)

GlassCrate
lightblue
00000
0...0
0...0
0...0
00000

(Bushes are opaque but not solid)
(idea: Bush + glass block = normal block?

Spirit hides in bushes
Bushes are destroyed when used by you or the spirit
Spirit leaves whenever you are one block away?

Maybe the spirit revives dead bushes it passes over? - would have to be when it stops moving. Maybe a different kind of bush?

Can you push the spirit with a crate? Nah

Maybe the main character is a fan? Has a Leaf blower?


idea: make it so that the spirit leaving the screen at a sand block opens a secret?
)

Bush
#4b916c #3e785a #579a65 #BD4242 darkgreen green
.....
.0...
021.0
10212
10102

DestroyedBush
#4b916c #3e785a #BD4242 green
.....
.....
.....
.....
0.01.

TempVacatedBush
black


SpiritLeftScreen
transparent

Water 
#3473a6 (#3B83BD) (#27A599)

BlockingU
transparent

BlockingD
transparent

BlockingR
transparent

BlockingL
transparent

Timer0
transparent

Timer1
transparent

Timer2
transparent

Timer3
transparent

Timer4
transparent

Timer5
transparent



=======
LEGEND
=======

. = Background
# = Wall
P = Player
* = StoneCrate
@ = StoneCrate and Target
% = StoneCrate and Bush
O = Target
S = Spirit
~ = Water
W = Spirit and Water
B = Bush
H = Bush and Spirit
J = Bush and Target
X = Player and Bush

G = GlassCrate
T = GlassWall

Blocking = BlockingU or BlockingD or BlockingR or BlockingL
Crate = StoneCrate or GlassCrate
Opaque = StoneCrate or Wall or Bush

Timer = Timer0 or Timer1 or Timer2 or Timer3 or Timer4 or Timer5 

SandIs = SandIsU or SandIsD or SandIsR or SandIsL


WaterCover = WaterCoverUp or WaterCoverDown or WaterCoverRight or WaterCoverLeft or WaterCoverUpRightCorner or WaterCoverDownRightCorner or WaterCoverUpLeftCorner or WaterCoverDownLeftCorner

StoneCrateSink = StoneCrateSink0 or StoneCrateSink1 or StoneCrateSink2 or StoneCrateSink3 or StoneCrateSink4 or StoneCrateSink5 or StoneCrateSink6 or StoneCrateSink7 or StoneCrateSink8 or StoneCrateSink9

=======
SOUNDS
=======

Crate MOVE 36772507

Crate destroy 45416100 (33195900) (58667903)

Endlevel 96976903

Spirit move 39212302
sfx2 39212302 (sound for when spirit has left screen)

Player move 74291507

sfx0 84286303 (96059500 31288108)  (7389303) (kick spirit out of bush)

sfx1 49801907 (26041300) (64859107)  () (destroy bush)

================
COLLISIONLAYERS
================

Background

Water

StoneCrateSink

WaterCoversDone,
WaterCoverUp,
WaterCoverDown,
WaterCoverRight,
WaterCoverLeft,   
WaterCoverUpRightCorner,   
WaterCoverDownRightCorner,   
WaterCoverUpLeftCorner,   
WaterCoverDownLeftCorner,



Target
Player, Wall, Crate, Spirit, GlassWall, GlassCrate
Bush, DestroyedBush

Blocking
TempVacatedBush
SpiritLeftScreen
Timer

SandIsU
SandIsD
SandIsR
SandIsL



======
RULES
======

(create side borders)
late up [ Water no WaterCoversDone | no Water ] -> [ Water WaterCoverUp | ]
late down [ Water no WaterCoversDone | no Water ] -> [ Water WaterCoverDown | ]
late right [ Water no WaterCoversDone | no Water ] -> [ Water WaterCoverRight | ]
late left [ Water no WaterCoversDone | no Water ] -> [ Water WaterCoverLeft | ]

(
(create corners)
late down [ Water no WaterCoversDone | ] -> [ Water WaterCoverDownRightCorner WaterCoverDownLeftCorner | ] 
late up [ Water no WaterCoversDone | ] -> [ Water WaterCoverUpRightCorner WaterCoverUpLeftCorner | ]

(remove extra corners)
late up [ Water WaterCoverUpRightCorner no WaterCoversDone | no WaterCoverRight ] -> [ Water | ] 
late right [ Water WaterCoverUpRightCorner no WaterCoversDone | no WaterCoverUp ] -> [ Water | ] 
late down [ Water WaterCoverDownRightCorner no WaterCoversDone | no WaterCoverRight ] -> [ Water | ] 
late right [ Water WaterCoverDownRightCorner no WaterCoversDone | no WaterCoverDown ] -> [ Water | ] 
late up [ Water WaterCoverUpLeftCorner no WaterCoversDone | no WaterCoverLeft ] -> [ Water | ]
late left [ Water WaterCoverUpLeftCorner no WaterCoversDone | no WaterCoverUp ] -> [ Water | ] 
late down [ Water WaterCoverDownLeftCorner no WaterCoversDone | no WaterCoverLeft ] -> [ Water | ] 
late left [ Water WaterCoverDownLeftCorner no WaterCoversDone | no WaterCoverDown ] -> [ Water | ] 
)
(finish covering Waters)
(late [ Water no WaterCoversDone ] -> [ Water WaterCoversDone])


(crate sinking animation)
[ StoneCrateSink9 ] -> []
[ StoneCrateSink8 ] -> [ StoneCrateSink9 ] 
[ StoneCrateSink7 ] -> [ StoneCrateSink8 ] 
[ StoneCrateSink6 ] -> [ StoneCrateSink7 ] 
[ StoneCrateSink5 ] -> [ StoneCrateSink6 ] 
[ StoneCrateSink4 ] -> [ StoneCrateSink5 ] 
[ StoneCrateSink3 ] -> [ StoneCrateSink4 ] 
[ StoneCrateSink2 ] -> [ StoneCrateSink3 ] 
[ StoneCrateSink1 ] -> [ StoneCrateSink2 ]
[ StoneCrateSink0 ] -> [ StoneCrateSink1 ]  (it looks jank with water covers - also  at the moment player has to wait if in line 										with spirit, due to again being triggered. Some way to avoid this?) 







[ > Player | Water ] -> [ Player | Water ] (don't let player enter water)
[ >  Player | Bush Crate ] -> [  Player | Bush TempVacatedBush > Crate ] sfx1 (push crate that's inside a bush, Akurra style. Should this destroy the bush?)
[ >  Player | Crate ] -> [  >  Player | > Crate ] (push crate)

([ > Player | Spirit Bush ] -> [ Player | > Spirit Bush ] sfx0 (kick spirit out of bush))
[ Player no Bush | Spirit Bush ] -> [ Player | > Spirit Bush TempVacatedBush] again (kick spirit out of bush)
late [ Player no Bush | Spirit Bush | no Crate no Wall ] -> [ Player no Bush | Spirit Bush | no Crate no Wall ]  sfx0 (play sound)

[ > Player Bush | Spirit ] -> [ > Player Bush | > Spirit] (kick spirit from bush - this is only useful with one spirit if the spirit is also in a bush)

[ > Player Bush ] -> [ > Player Bush TempVacatedBush] (kill bush that you walked through)

late [TempVacatedBush Bush no Player no Spirit no Crate ] -> [ DestroyedBush ] sfx1 (destroy bushes if there is nothing occupying them)
late [TempVacatedBush] -> []

(late [Player Bush] -> [Player Bush]  (player bush sound))

late [Crate Water] -> [StoneCrateSink0 Water] (again) (destroy crate in water)
(late [Crate Bush] -> [Crate DestroyedBush] sfx1)


(check lines of sight from spirit)
[Blocking] -> []
up [ Spirit | ... | Opaque ] -> [ Spirit | ... | Opaque BlockingU]
down [ Spirit | ... | Opaque ] -> [ Spirit | ... | Opaque BlockingD]
right [ Spirit | ... | Opaque ] -> [ Spirit | ... | Opaque BlockingR]
left [ Spirit | ... | Opaque ] -> [ Spirit | ... | Opaque BlockingL]

(make spirit move away)
[ Player no Bush | ... | Spirit no Bush | no wall no crate Background] -> [ Player | ... | > Spirit | no wall no crate Background]
(cancel spirit movement if line of sight blocked) 
up [up Spirit][ Player | ... | BlockingD ] ->  [ stationary Spirit][ Player | ... | BlockingD ]
down [down Spirit][ Player | ... | BlockingU ] ->  [ stationary Spirit][ Player | ... | BlockingU ] 
right [right Spirit][ Player | ... | BlockingL ] ->  [ stationary Spirit][ Player | ... | BlockingL ] 
left [left Spirit][ Player | ... | BlockingR ] ->  [ stationary Spirit][ Player | ... | BlockingR ] 
 

[stationary Spirit Target] -> win

late [Player | ... | Spirit] -> [Player | ... | Spirit] again


(delete the spirit if it gets to the edge of the screen)

[ Spirit ] -> [ Spirit SpiritLeftScreen ]
up [ Spirit SpiritLeftScreen | ] -> [ Spirit | ]
[ SpiritLeftScreen Spirit ] -> [ SpiritLeftScreen ]

[ Spirit ] -> [ Spirit SpiritLeftScreen ]
down [ Spirit SpiritLeftScreen | ] -> [ Spirit | ]
[ SpiritLeftScreen Spirit ] -> [ SpiritLeftScreen ]

[ Spirit ] -> [ Spirit SpiritLeftScreen ]
right [ Spirit SpiritLeftScreen | ] -> [ Spirit | ]
[ SpiritLeftScreen Spirit ] -> [ SpiritLeftScreen ]

[ Spirit ] -> [ Spirit SpiritLeftScreen ]
left [ Spirit SpiritLeftScreen | ] -> [ Spirit | ]
[ SpiritLeftScreen Spirit ] -> [ SpiritLeftScreen ]

(delay to let running sounds keep going after spirit leaves the screen)
[ Timer5 ] -> []
[ Timer4 ] -> [ Timer5 ] sfx2
[ Timer3 ] -> [ Timer4 ]
[ Timer2 ] -> [ Timer3 ]
[ Timer1 ] -> [ Timer2 ] sfx2
[ Timer0 ] -> [ Timer1 ]
[SpiritLeftScreen] -> [ Timer0 ] sfx2



==============
WINCONDITIONS
==============


=======
LEVELS
=======
(
message Every year, biologists ___ the rare ____ . But they have to catch them first.

messages can be about the strange behaviours/properties of the species.
)

~~~~~~~~~~
~~~#...#.~
~~.#o....~
~........~
~........~
~~#.s...~~
~~......#~
~.....p.#~
~~.###...~
~~~~~~~~~~

~~~~~~~~~~~
~~~~~~~..~~
~~~###...~~
~~~....#.~~
~~...p....~
~~........~
~~..s.o#..~
~##...##.~~
~~.......~~
~~~.....~~~
~~~~~~~~~~~


~~~~~~~~~~~~
~~~~####~~~~
~####..o~~~~
~~........~~
~~.#..s...~~
~~......p.~~
~~~##...###~
~~~~#.#.#~~~
~~~~~...~~~~
~~~~~~~~~~~~

~~~~~~~~~~~~
~~~....##~~~
~~#.#.....~~
~#...~.#...~
~#.#.~~.s..~
~~..~~~..p.~
~~~..~~~.##~
~~~~.~~~o#~~
~~~~#~#~~#~~
~~~~~~#~~~~~
~~~~~~~~~~~~


~~~~~~~~~~~~~~~
~~~#~..~..~~~~~
~.~#.......~~~~
~~~#........~~~
~~.#o.*s..#.~~~
~~~.......#..~~
~.~#.p.......~~
~.~~~......~~~~
~~~~~~#.~~~~#~~
~~~~~~~~~~~~~~~

~~~~~~~~~~~~~
~~~~~..~~~~~~
~~~~.....###~
~~###.....o#~
~~#........#~
~.#..s......~
~...........~
~..*....##.~~
~~....~.##.~~
~~.p.~~....~~
~~~~~~~~~~~~~

~~~~~~~~~~#~~~
~#~~~~~~~~~#~~
~~~~~....~~~~~
~~~...*..~~#~~
~~....o...~~#~
~~..p.*...~~#~
~~.......~~~#~
#~....s..~~#~~
~~~......~~#~~
~#~~~~..~~~~#~
~#~~~~~~~~#~~~

.....~~~~~~~~
#########~~~~
#.......#.~~~
#.*~~~.o#...~
#..~~~......~
#..~~~..s...~
#..~~~......~
#.*~~~.p#...~
#.......#..~~
#########.~~~

~~~~~~~~~~~~~~~
~~...~~~~~..#~~
~~.s.~~~~~..#~~
~~...~~~~~..#~~
~~...~~~~~..#~~
~~...#...#..#~~
~~.o.*.p.*..#~~
~~..........#~~
~~~~~~~~~~~~~~~ (send back the way it came with crate - make this clearer somehow?)

.b..b~~~~~~~~
##.######~~~~
#.......#.~~~
#.*~~~.o....~
#..~~~....#.~
#..~~~..s...~
#..~~~.....#~
#.*~~~.p#...~
#.......#b.~~
#########.~~~


(3 crates?)

~~~~~~~~~~~~~
~~~~~....~~~~
~~.......~~~~
~#*##*#....~~
~..*..#~...#~
~p.#..#~~..#~
~..#.~~~w*.#~
~~.#~~~#~~..~
~~~#~~~#*...~
~~~~~#~.o.#~~
~~~~~~~~#~~~~
~~~~~~~~~~~~~  (level introducing destroying crates)


####~~~######
##~~~~~~~...#
#...**.*....#
#po*.**...s.#
#..*..*.*...#
##~~~~~~~...#
####~~~######  (destroy crates that are in the way) (must have at least one crate on each side)(must have one in middle before back crate in place) (would be nice to have this be more deductive but difficult to design)




(put some bushes you must break in earlier levels, to show that you can)





(levels introducing bushes as limited use cover)

~~~~~~~~~~~~~~~~
~~~~#~~~~~~~~~~~
~~#~~~.b..~~~~~~
~~~~#...b.b~#~~~
~~~~##b##....~~~
~~~......~.#b.~~
~~#.#os.b~~~..~~
~~#.....b~~.#.~~
~~##.....~~..b~~
~~~~..##~~~p.~~~
~~~~~#~~~~~~~~~~
~~~~~~~~~~~~~~~~

(
~~~~~~~~~~~~~~~~
~~~~#~~~~~~~~~~~
~~#~~~.b..~~~~~~
~~~~#...b.b~#~~~
~~~~##b##....~~~
~~~......~.#b.~~
~~#.#.s.b~~~..~~
~~#.....b~#...~~
~~##.....~~..b~~
~~~~..##~~~p.~~~
~~~~~#~~~o~~~~~~
~~~~~~~~~#~~~~~~
~~~~~~~~~~~~~~~~ (simplify to put focus on the important discovery?))

~~~~~~~~~~~~~~~
~~~~#~~#~~~~~~~
~~~...~#~~~~~~~
~~..#~~~~~~b~~~
~~b.#~#~~##.#~~
~~....#~..b..~~
~b.#..........~
~..b....s.....~
~..........#..~
~..p.bo###...~~
~~b..####b.~~~~
~~~...~~~~~~~~~
~~~~~~~~~~~~~~~


(levels introducing bushes as catching spirit)


~~~~~~~~~~~~~~
~~~~~...~~~~~~
~~....b.#~##~~
~~.b.........~
~............~
~#b...s...#..~
~#.bo#.......~
~#..b~~b...b~~
~~.p.#~~~...~~
~~~~##~~#~~~~~
~~~~~~~~~~~~~~ (makes it look like bushes will stop spirit like walls) (maybe add an animation for entering bush for the first time?)


~~~~~~~~~~~
~#~...~...~
~~~.~.~.~.~
~..b#b.h..~
~.~.~.~.~.~
~p.b.b.b..~
~.~.~.~.~.~
~..j.b#b..~
~.~.~.~.~~~
~...~...~#~
~~~~~~~~~~~

~~~~~~~~~~~~~~~~~
~~~~~~~~~......~~
~~....~....~~...~
~...b.~~.b..~b..~
~~~....~~~~.~~~~~
~~........~.~..~~
~~~.b..b.w~..b..~
~~~~~~....~~...~~
~~~..~.........~~
~...b..o.b.p.b~~~
~~.....#....~~~~~
~~~~~~~~~~~~~~~~~ (can't walk adjacent to bush w/ spirit (must be on correct side of it). Use Bush as Cover.)

(
~~~~~~~~~
~~.....~~
~.b.b#h.~
~..b.j..~
~.#.b.b.~
~..b.b..~
~.b.b.b.~
~~...p.~~
~~~~~~~~~
)





(level where you must leave at least one bush per col as cover?)





(intentionally break bushes)

~~~~~~~~~~~~
~~~.#.~~~~~~
~~......o#~~
~~.#b~.....~
~#.~~~~....~
~...~~~~...~
~~..s.~~b.#~
~~p.......~~
~~~.b...#~~~
~~~~~~~~~~~~ (bush you can't break and run until moving the spirit)(forces you to do a circuit)


~~~~~~~~~~~~~~~~~~~
#o~b~.b.~~~~~~~~~~~
~.....~.~~~~~~~~~~~
#.#~~~~b~.b.h.~b~b.
#.#~......~...~.~..
#..b...##.~.p...~..
#####..##.#........  (intentionally breaking bushes, and progress gated by bushes that must be used constructively)


~~~~~~~~~~~~~
~####~#~...#~
~#o..~b~w~b#~
~#.~...~~~.#~
~~.~~~~~b...~
~~.~~~~~~~~.~
~#.#b.b..b~.~
~#.~~.~.#~~.~
~#...p......~
~~~~~~~~~~~~~ (intentionally break so it won't block line of sight)(break when in view of spirit / break and go back)


(
~~~~~~~~~~~~~~~
~~~~#~~~~~~~~~~
~~#~##~~~~~.~~~
~~#~..~~~b~o#~~
~~~~.bb.~b...~~
~#~~...~~..p..~
~~~~~..~......~
~.#~.#b..s.#.~~
~..###...b...~~
~~.##~~..#...~~
~~#.#~~#~~#.~~~
~~~~~~#~~~~~~~~
~~~~~~~~~~~~~~~ (breaking bushes)(focus on double bushes) (two bushes are red herrings. Is this bad?)
)




(hiding in bush adjacent to spirit in bush)

~~~~~~~~~~~~~
~...~~~~~~~~~
~.~.~~~~...~~
~.bb..p~.bb~~
~~~~.~...b.~~
~#bb.h.b..~~~
~.~.~#~#~o~~~
~...~#..~#~~~
~~~~~~~~~~~~~



~~~~~~~~~~~~
~~##~#~~j~~~
~.bb..~..bb~
~.##~~~....~
~....s.....~
~~~~~x~~~~~~
~~~~~~~~~~~~

~~~~~~~~~~~~
~..#o...b..~
~.bh.p..b..~
~.....#.b..~
~~~~~~~~~~~~

#########
~o~~~~~p~
~.bbbbb.~
~~.~b~.~~
~~.~h~.~~
~.bbbbb.~
~.~~~~~.~
#########

~~~~~~#~~~
~~~~~~o~~~
~~~~~~~~~~
~~~.bb~~~~
~.......~~
~.#.s...~~
~.p.....~~
~~~bbb~~~~
~~~...~~~~
~~~~~~~~~~

~~~~#~~~~~
~~~~o~~~~~
~~~~~~~~~~
~.bb~~~~~~
~...~~##~~
~#....hb.~
~..p..##.~
~...~....~
~bbb~~~~~~
~...~~~~~~
~~~~~~~~~~

~~~~~~~~~~~~~
~~~##~~~~###~
~###o.b.p..#~
~#.....~...#~
~#bb..b.bhb#~
~#bb...~~~##~
~##~~~~~~~~~~
~~~~~~~~~~~~~ (ok)

~~~~~~~~~~~~~~~~~~~~
~#~~~~~~~~~~~~#~#~~~
~~~..b......b.#~~~~~
~xs..b.~b...b.#o...~
~##..b....#.b....b.~
~~~~~~~~~~#......b.~
~~~~~~~~#~#~~~~~~~~~
~~~~~~~~~~~~~~~~~~~~

~~~~~~~~~~~~~~~~
~~~~~~~#..~~~~~~
~~...b......b.~~
~xs..b.~b...b.~~
~##..b....#...#~
~#..~~~..~#..o#~
~~#~~~~~#~#~~##~
~~~~~~~~~~~~~~~~

~~~~~~~~~~~~~~~~~~~~~
~~~~~~~..#.~~~~#~##~~
~~...b...#..b....o#~~
~xs..b.~b...b.#...~~~
~##..b....#.b...bb#~~
~#..~~~..~#.....~##~~
~~#~~~~~#~#~~###~~~~~
~~~~~~~~~~~~~~~~~~~~~

~~~~~~~~~~~~~~~~~~~~
~~~~~~~..#.~~~#~~~~~
~~...b...#..b....o#~
~xs..b.~b...b.#~b.#~
~##..b....#.b...b.~~
~#..~~~..~#.....b.~~
~~#~~~~~#~#~~###~~~~
~~~~~~~~~~~~~~~~~~~~ (ok but has a red herring)

~~~~~~~~~~~~~~~~~~
~~~~~~~..#.~~~###~
~~...b...#..b..o#~
~xs..b.~b...b.~.~~
~##..b....#.b...~~
~#..~~~..~#.....#~
~~#~~~~~#~#~~##~~~
~~~~~~~~~~~~~~~~~~ (pretty good)










(double bushes and pushing spirit free)

~~~~~~~~~~~~~~~
~~#~~~..~~~~#~~
~~~~.bb.~..~#~~
~~.......b.~~~~
~#.b.b...b..b.~
~..b.b.p....h.~
~.............~
~~##..obb..#~~~
~~~~~~####~~~~~
~~~~~~~~~~~~~~~




(line of bushes)

~~~~~~~~~~~~~
~~~.#.....~~~
~.....p.#...~
~.hbbbbbbbo.~
~..#.......#~
~.....#.....~
~~~~#....~~~~
~~#~~~~#~~~~~
~~~~~~~~~~~~~



(pushing spirit in field of bushes)

~~~~~~~~~
~.bbbbb.~
~p~hjbb.~
~~bb~b~~~
~.bbbbb.~
~...~...~
~~~~~~~~~


(intentionally breaking + hiding in bush next to spirit)
~~~~~~~~~~~
~~~.b.~...~
~.bb~b..j.~
~.~.b.~...~
~...~~~~~~~
~~~b....b.~
~..b.b~bs.~
~b~b.~~.~.~
~.~~~~~.#.~
~...p.....~
~~~~~~~~~~~




(crates and bushes)

~~~~~~~~~~~~~
~~.....~~~#~~
~.h.*.b..~~~~
~.........~#~
~.....b.b.~~~
~.....~~..~~~
~~~.p.~~bo#~~
~#~...~~~~~#~
~~~~~~~~~~~~~




(place crate next to spirit in bush so you can walk past without breaking it)

~~~~~~~~~~~~~~~~~
~...............~
~.b.p.h.b.*..bb.~
~.......#.#.o...~
~~#~~~#~#~#~#~~~~
~~~~~~~~~~~~~~~~~



(Push crate in bush to get it past spirit)

~~~~~~~~~~~~~~
~~~~~~~####~~~
~~...#~o..~~~~
~~........~~~~
~##b..s..b##~~
~~...##~#..~~~
~~.........~~~
~~..*.p.b..##~
~#####~~#..~~~
~~~~~~~~~~~~~~


(
~~~~~~~~~~~~~
~~.......#~#~
~~........~~~
~~.s.b~~bb.~~
~##........#~
~~~..p.*.o~#~
~~#~.....#~~~
~~~~~~~~~~~~~
)


~~~~~~~~~~~~~~
~#~~~~...~#~~~
~~.....b.~~~#~
~#.........o#~
~#..#b.s.b.#~~
~#~#.......~#~
~~~~.*.#.p.#~~
~~~#.......~~~
~~~~~~~~~~~~~~

~~~~~~~~~~~~~~
~~~~~~...~~~~~
~~.......~#~~~
~~..~~.b.~~~#~
~#.........o#~
~#..bb.s.b##~~
~#~#..~....~~~
~~~~~.##...~#~
~~~#~....*.~~~
~~~~~.p....~~~
~~~~~~~~~~~~~~

(push crate in bush so that spirit can hit it)

~~~~~~~~~~~~
~~~#.......~
~#~#.p...%.~
~~~#.....w.~
~~~~~~.~~~.~
~#o.b~b~...~
~#.~.~.#.##~
~#.~.....##~
~~~~~~~~~~~~


(Push crate in bush to get it past spirit in bush)

(break bush with crate intentionally? - much easier to force if there are multiple spirits)

~~~~~~~~~
~...b...~
~...s.b.~
~~~~~~..~
~..~.~..~
~.j~b~b.~
~..~.~..~
~..*....~
~....p..~
~~~~~~~~~

~~~~~~~~~
~~~~~...~
~..bb*..~
~.*.s...~
~..~~#..~
~j.bb~b.~
~~~~b~..~
~~~~.~..~
~~.*....~
~~...p..~
~~~~~~~~~ (almost something?)
















(misclevels)


~~~~~~~~~~~~~~~
~~~...~~~....~~
~~..b...~b.b..~
~~.b..b......b~
~#..#o....s..#~
~#.........#~~~
~....p.b.b..~~~
~~b#.........~~
~~~#..b...b.#~~
~~~##.......~~~
~~~~~~~#...~~~~
~~~~~~~~~~~~~~~ 


~~~~~~~~~
~~...*.#~
~~*..*.#~
~#*~.#~~~
~..*so.*~
~p#.....~
~.*.~**.~
~~##~~##~
~~~~~~~~~

~~~~~~~~~~~
~~~~#~~~~~~
~~...~...~~
~~.......~~
~~..#..~~~~
~...#..s~~~
~....o....~
~##*#*#.#.~
~~..#.....~
~~....p~~~~
~~~.#~~~~~~
~~~~~~~~~~~ (meh?) (fill gaps)

~~~~~~~~~~~~~~~~~~~
~~.#....~~~~~~~~~~~
~.....p.#~~#....~~~
~..~~~...~~.....#~~
~...~~~..~~..~#...~
~.b.~~~.~~...~~.o#~
~....~..~~...~~...~
~.s.#~...~..~#~...~
~~..~~......#~~#..~
~~...~~#....~~...~~
~~~~~~~~~~~~~~~~~~~ 

~~~~~~~~~~~~
~~~~~~...~~~
~~~......~.~
~#~*##*#.~~~
~~..*..#..~~
~#p.#..#..~~
~#..#~~.s..~
~~#~~......~
~#o.~~....~~
~####~~#...~
~~~~~~~~~~~~



~~~~~~~~~~~~~~~~~
~~~~~...~~~~~~~~~
~~~~~....~~~..~~~
~~~~....s~....~~~
~~~~.*...~~~~..~~
~~~~.#.*....~~..~
~#o.........~~.~~
~~~~~.~~....~~.~~
~~~~~p....~~~..~~
~~~~~~~~.~~...~~~
~~~~~~~~..~..~~~~
~~~~~~~~....~~~~~
~~~~~~~~~~~~~~~~~





(misc non-levels)


~~~~~~~~~~~
~~~~~~####~
~~~~~~#...~
~####~#*..~
~...#~#...~
~....s....~
~.........~
~..*.#....~
~.........~
~.p.......~
~~~~~~~~~~~
~~~~~~~~~~~ (level where you recover box)





~~~~~~~~~~~~~
~~~~~~###~~~~
~~~~~#.o.~~~~
~~~.......~~~
~~~.#...s.~~~
~~........#~~
~~.#.......~~
~~......#..~~
~~#.p......~~
~~~......#.~~
~~~~~~~~~~~~~
~~~~~~~~~~~~~

~~~~~~~~~~~~~~~
~~~~~.#....~~~~
~~~.....#.....~
~~........#...~
~..b........#.~
~.....s....b..~
~.b.p......#..~
~......b.#.~~~~
~~..b..#...~~~~
~~~...~~~~~~~~~
~~~~~~~~~~~~~~~


~~~~~~~~~~~~~~~
~b.b.b~#~b.b.b~
~.~.~.~o~.~.~.~
~b.b.h.b.b.b.b~
~.~.~.~~~.~.~.~
~b.b.b.b.b.b.p~
~.~.~.~~~.~.~.~
~b.b.b~~~b.b.b~
~~~~~~~~~~~~~~~


...............
.~.b.~.b.~.b.~.
b.b~b.b~b.b~b.b
~.~~~.~~~.~~~.~
~b.b.b~w~b.b.b~
~~~.~~~~~~~.~~~
~~~p...b....~~~
~~~~~~~~~~~~~~~

~~~~~~~~~~~~~
~...........~
~...b....b..~
~.b....b....~
~....b....b.~
~..b....b...~
~.....b.....~
~.p.b....b..~
~...........~
~~~~~~~~~~~~~









~~~~~~~~~~~~~
~~~.....~~~~~
~~........~~~
~..........~~
~..g.s..o#.~~
~..........~~
~...#......~~
~~.........~~
~~~....p..~~~
~~~~~~~~~~~~~

~~~~~~~~~~~~~~
~~~~~~~~~..~~~
~~~~........~~
~~......g...~~
~~..........~~
~~~...s...o.~~
~~~.........~~
~~~~....*...~~
~~~~~.......~~
~~~~~..p...~~~
~~~~~~~~~~~~~~

~~~~~~~~~~~~~~~
~~..........~~~
~.......##...~~
~##.....##...~~
~##.to....t..~~
~...t..s..t..~~
~...t.....t..~~
~...##.......~~
~...##...p..~~~
~~.........~~~~
~~~~~~~~~~~~~~~


~~~~~~~~~~~~~~
~~~..........~
~~...........~
~~.......g.p.~
~..#.........~
~.........*o.~
~......s.....~
~...#........~
~~...........~
~~~.........~~
~~~~~~~~~~~~~~