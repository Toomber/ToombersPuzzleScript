title EpicJamGame
author Toombler
homepage www.puzzlescript.net

again_interval 0.05
realtime_interval 0.1
noaction
run_rules_on_level_start


(verbose_logging)

========
OBJECTS
========

Background
#CCDCEA #99C6E2  #ffaf47 #ffbf69
11011
01110
11001
11100
00111

WaterCoverUp
#7fb0d7 #4089c4 (#96bede #67a1d0) (#95e6de #2ec2b4)
00000
11111
.....
.....
.....

Target
#445c75 #1b2632 darkblue (#ba6eac)
.....
.000.
.000.
.000.
.....

Wall
#3a3c3a #505451 #6a6e6b #8c908c
20203
22302
02312
03010
20010

NormalPlayer
#383430 #A09D95 #6D6C69 #574236
.000.
.111.
33333
02220
.0.0.

StoneCrate
#a05f75 #a35c68 #b14e6d #94555c 
02322
3...2
3...2
3...1
33331

StoneCrateSinkCover0
#966179 #995E6E #A65172 #8B5863 #9D667E #AC5777 #925D67 #A75375 #8C5A65
45655
8...7
3...2
3...1
33331

StoneCrateSinkCover1
#8C637E #8F6073 #9A5577 #835A69 #9A6E87 #A86080 #906672 #9C597D #855E6F
45655
8...7
3...2
3...1
33331

StoneCrateSinkCover2
#836482 #856279 #8F587D #7A5D70 #977590 #A3698A #8E6E7E #925E85 #7D6378
45655
8...7
3...2
3...1
33331

StoneCrateSinkCover3
#796687 #7B647F #845B82 #716077 #947C99 #9F7294 #8C7689 #88638D #756882
45655
8...7
3...2
3...1
33331

StoneCrateSinkCover4   
#6F688B #716684 #785F87 #68637E #9184A2 #9A7B9D #8A7E94 #7E6995 #6E6D8B
45655
8...7
3...2
3...1
33331

StoneCrateSinkCover5
#656A90 #66698A #6D628C #606584 #8E8BAA #9683A7 #89879F #736E9C #667195
45655
8...7
3...2
3...1
33331

StoneCrateSinkCover6
#5B6C94 #5C6B8F #616691 #57688B #8B93B3 #918CB0 #878FAA #6974A4 #5F769E
45655
8...7
3...2
3...1
33331

StoneCrateSinkCover7
#516E99 #526D95 #566996 #4E6B92 #889ABC #8D95BA #8597B5 #5F79AC #577BA8
45655
8...7
3...2
3...1
33331

StoneCrateSinkCover8
#486F9D #486F9B #4B6C9C #456E99 #85A1C5 #889EC4 #839FC1 #557EB4 #4F80B1
45655
8...7
3...2
3...1
33331

StoneCrateSinkCover9
#3E71A2 #3E71A0 #3F70A1 #3D709F #82A9CE #84A7CD #81A8CC #4A84BC #4884BB
45655
8...7
3...2
3...1
33331




StoneCrateSink0
#966179 #995E6E #A65172 #8B5863
02322
3...2
3...2
3...1
33331

StoneCrateSink1
#8C637E #8F6073 #9A5577 #835A69
02322
3...2
3...2
3...1
33331

StoneCrateSink2
#836482 #856279 #8F587D #7A5D70
02322
3...2
3...2
3...1
33331

StoneCrateSink3
#796687 #7B647F #845B82 #716077
02322
3...2
3...2
3...1
33331

StoneCrateSink4
#6F688B #716684 #785F87 #68637E
02322
3...2
3...2
3...1
33331

StoneCrateSink5
#656A90 #66698A #6D628C #606584
02322
3...2
3...2
3...1
33331

StoneCrateSink6
#5B6C94 #5C6B8F #616691 #57688B
02322
3...2
3...2
3...1
33331

StoneCrateSink7
#516E99 #526D95 #566996 #4E6B92
02322
3...2
3...2
3...1
33331

StoneCrateSink8
#486F9D #486F9B #4B6C9C #456E99
02322
3...2
3...2
3...1
33331

StoneCrateSink9
#3E71A2 #3E71A0 #3F70A1 #3D709F
02322
3...2
3...2
3...1
33331

SpiritStationary
Purple
.....
.000.
.000.
.000.
.....

SpiritHitsU
Purple
00000
00000
.....
.....
.....

SpiritHitsD
Purple
.....
.....
.....
00000
00000

SpiritHitsR
Purple
...00
...00
...00
...00
...00

SpiritHitsL
Purple
00...
00...
00...
00...
00...

SpiritMovesU
Purple
.000.
.000.
.000.
.000.
.....

SpiritMovesD
Purple
.....
.000.
.000.
.000.
.000.


SpiritMovesR
Purple
.....
.0000
.0000
.0000
.....

SpiritMovesL
Purple
.....
0000.
0000.
0000.
.....

Bush0A
#4b916c #3e785a #579a65
.....
.....
.0...
021.0
10212

Bush0B
#4b916c #3e785a #579a65
.....
.....
...0.
0.120
21201

Bush1A
#4b916c #3e785a #579a65
.....
.....
..22.
.1012
12110

Bush1B
#4b916c #3e785a #579a65
.....
.....
.22..
2101.
01121

Bush2A
#4b916c #3e785a #579a65
.....
.0...
.020.
1210.
11210

Bush2B
#4b916c #3e785a #579a65
.....
...0.
.020.
.0121
01211


DestroyedBush0
#54a199 #4f9c99 #4f633b #325c4e #4b916c #3e785a
.....
.....
.....
.....
0.01.

DestroyedBush1
#54a199 #4f9c99 #4f633b #325c4e #4b916c #3e785a
.....
.....
.....
.....
1..0.

DestroyedBush2
#54a199 #4f9c99 #4f633b #325c4e #4b916c #3e785a
.....
.....
.....
.....
01.1.

BushHasBeenRandomised
transparent

TempVacatedBush
black

TempSpiritAlreadyCollided
transparent

SpiritLeftScreen
transparent

CinematicWin
transparent

CinematicWall
#3a3c3a #505451 #6a6e6b #8c908c
20203
22302
02312
03010
20010

CinematicPlayer
transparent

CinematicPlayerSwap
transparent

CinematicPlayerPushed
#383430 #A09D95 #6D6C69 #574236
.000.
.111.
33333
02220
.0.0.

SpiritLightMarker
transparent

Water 
#3473a6 (#3B83BD) (#27A599)

SpiritScaredMarker
transparent

BlockingU
transparent

BlockingD
transparent

BlockingR
transparent

BlockingL
transparent

Timer0
transparent

Timer1
transparent

Timer2
transparent

Timer3
transparent

Timer4
transparent

Timer5
transparent


WaterDepthneg1
#397cb3

WaterDepth0
#3473a6 

ManualWaterDepth0
#3473a6 

WaterDepth1
#2d6491

WaterDepth2
#285a82

WaterDepth3
#25547a

WaterDepth4
#204a6b

ManualWaterDepth4
#204a6b

WaterDepth5
#1c405c

WaterDepth6
#17354d

WaterDepth7
#112738

WaterDepth8
#0f2230

=======
LEGEND
=======

. = Background
# = Wall

P = NormalPlayer
F = Water and CinematicPlayer
I = Water and CinematicPlayerSwap
Player = NormalPlayer or CinematicPlayer
* = StoneCrate
@ = StoneCrate and Target
% = StoneCrate and Bush1A
O = Target
S = SpiritStationary and TempSpiritAlreadyCollided
~ = Water
W = SpiritStationary and Water and TempSpiritAlreadyCollided
B = Bush1A
H = Bush1A and SpiritStationary and TempSpiritAlreadyCollided
J = Bush1A and Target
; = DestroyedBush0
+ = DestroyedBush1
' = DestroyedBush2
X = NormalPlayer and Bush1A

C = Water and CinematicWin
U = CinematicWall
{ = CinematicPlayerPushed
& = CinematicPlayerPushed and Water


L = water and ManualWaterDepth0
D = water and ManualWaterDepth4
WaterDepth = WaterDepthneg1 or WaterDepth0 or WaterDepth1 or WaterDepth2 or WaterDepth3 or WaterDepth4 or WaterDepth5 or WaterDepth6 or WaterDepth7 or WaterDepth8 


Blocking = BlockingU or BlockingD or BlockingR or BlockingL
SpiritHits = SpiritHitsU or SpiritHitsD or SpiritHitsR or SpiritHitsL
SpiritMoves = SpiritMovesU or SpiritMovesD or SpiritMovesR or SpiritMovesL
Spirit = SpiritStationary or SpiritHits or SpiritMoves
Crate = StoneCrate
Bush = Bush0A or Bush1A or Bush2A or Bush0B or Bush1B or Bush2B
DestroyedBush = DestroyedBush0 or DestroyedBush1 or DestroyedBush2
Opaque = StoneCrate or Wall or Bush

Timer = Timer0 or Timer1 or Timer2 or Timer3 or Timer4 or Timer5 

StoneCrateSink = StoneCrateSink0 or StoneCrateSink1 or StoneCrateSink2 or StoneCrateSink3 or StoneCrateSink4 or StoneCrateSink5 or StoneCrateSink6 or StoneCrateSink7 or StoneCrateSink8 or StoneCrateSink9
StoneCrateSinkCover = StoneCrateSinkCover0 or StoneCrateSinkCover1 or StoneCrateSinkCover2 or StoneCrateSinkCover3 or StoneCrateSinkCover4 or StoneCrateSinkCover5 or StoneCrateSinkCover6 or StoneCrateSinkCover7 or StoneCrateSinkCover8 or StoneCrateSinkCover9  

=======
SOUNDS
=======

Crate MOVE 36772507

Crate destroy 45416100 (33195900) (58667903)

CinematicWall destroy 73561902 (11850702)

(Endlevel 96976903)
sfx7 96976903

(Spirit move 50223502 (39212302))

SpiritMoves create 66783707 (31766707) (13235302) (8021307) (spirit scared)

sfx2 9086109 (71499902) (17662302) (39212302) (sound for when spirit has left screen)

Player move 78892307 (23662107) (16418907) (74291507)

sfx0  15065104      (kick spirit out of bush)
(
7389303
96059500
49635507
31288108)


sfx1 49801907 (26041300) (64859107)  () (destroy bush)
(
9249708
62997108
86294708)

sfx3 2447107 (spirit collides with wall)

sfx4 33912304 (71683100) (76311307) (spirit collides with crate)
sfx5 13888100 (63113700) (spirit collides with crate 2)



sfx6 60883704 (51866502) (49962304) (spirit enter bush sound)


================
COLLISIONLAYERS
================

Background

Water



ManualWaterDepth0
ManualWaterDepth4
WaterDepth
WaterCoverUp
StoneCrateSink, StoneCrateSinkCover

Target
DestroyedBush
Player, Wall, CinematicWall, Crate, Spirit,
Bush

Blocking
SpiritScaredMarker
TempVacatedBush
TempSpiritAlreadyCollided
SpiritLeftScreen
CinematicWin
CinematicPlayerSwap
CinematicPlayerPushed
SpiritLightMarker
Timer
BushHasBeenRandomised



======
RULES
======


[ > Player | Water ] -> [ Player | Water ] (don't let player enter water)
([ >  Player | Bush Crate ] -> [  Player | Bush TempVacatedBush > Crate ] sfx1 (push crate that's inside a bush, Akurra style))
[ >  Player | Crate ] -> [  >  Player | > Crate ] (push crate)


[ > Player Bush | Spirit ] -> [ > Player Bush | > Spirit] (kick spirit from bush - this is only useful with one spirit if the spirit is also in a bush)

late [ Player no Bush | Spirit Bush | no Crate no Wall ] -> [ Player no Bush | Spirit random DestroyedBush | no Crate no Wall ]  sfx0 sfx1 (kick spirit out of bush)


[ > Player Bush ] -> [ > Player Bush TempVacatedBush] (kill bush that you walked through)

late [TempVacatedBush Bush no Player no Spirit no Crate ] -> [ random DestroyedBush ] sfx1 (destroy marked bushes if there is nothing occupying them)
late [TempVacatedBush] -> []

(destroy crates in water)
late [Crate Water WaterCoverUp] -> [StoneCrateSinkCover0 Water WaterCoverUp]
late [Crate Water] -> [StoneCrateSink0 Water]


(move spirit away from player if marked to do so)
[ Player | ... | Spirit SpiritScaredMarker ] -> [ Player | ... | > Spirit ] 
[SpiritScaredMarker] -> [] 

(check lines of sight from spirit)
late [Blocking] -> []
late up [ Spirit | ... | Opaque ] -> [ Spirit | ... | Opaque BlockingU]
late down [ Spirit | ... | Opaque ] -> [ Spirit | ... | Opaque BlockingD]
late right [ Spirit | ... | Opaque ] -> [ Spirit | ... | Opaque BlockingR]
late left [ Spirit | ... | Opaque ] -> [ Spirit | ... | Opaque BlockingL]


(make spirit move away)

late [ Player no Bush | ... | Spirit no Bush | no wall no crate ] -> [ Player | ... | Spirit SpiritScaredMarker | ] 


(cancel spirit movement if line of sight blocked) 

late up 	[ Player no Bush | ... | Spirit SpiritScaredMarker no Bush ][ Player | ... | BlockingD ]  ->  [ Player | ... | Spirit ][ Player | ... | BlockingD ]
late down 	[ Player no Bush | ... | Spirit SpiritScaredMarker no Bush ][ Player | ... | BlockingU ]  ->  [ Player | ... | Spirit ][ Player | ... | BlockingU ]
late right 	[ Player no Bush | ... | Spirit SpiritScaredMarker no Bush ][ Player | ... | BlockingL ]  ->  [ Player | ... | Spirit ][ Player | ... | BlockingL ]
late left	[ Player no Bush | ... | Spirit SpiritScaredMarker no Bush ][ Player | ... | BlockingR ]  ->  [ Player | ... | Spirit ][ Player | ... | BlockingR ]


(if spirit dues to move next turn, play it immediately)
late [SpiritScaredMarker] -> again









(-----------------cinematics---------------)

(cinematic win - needs to be before edge of screen detection)
[ Spirit CinematicWin ] -> [] win

(bounce spirit cinematic)
right [ CinematicPlayer ][ CinematicPlayerSwap ][ spirit | CinematicWall ] -> [  ][ CinematicPlayer ][ left spirit | CinematicWall ] 

(cinematic detroy wall)
[ > Spirit | CinematicWall ] -> [ > Spirit | water ]
down [ Spirit | WaterCoverUp ] -> [ Spirit | ]

(Push player)
[ CinematicPlayerPushed | Spirit ] -> [ < CinematicPlayerPushed | Spirit ]
[ CinematicPlayerPushed CinematicWin ] -> [ CinematicWin ]

[ > CinematicPlayerPushed | Crate ] -> [ > CinematicPlayerPushed | > Crate ]  


(------------------------------------------)


(----------------- Aesthetics ---------------------)

(bush randomisation)
[ Bush no BushHasBeenRandomised ] -> [ random Bush BushHasBeenRandomised] checkpoint


(create side borders)
up [ Water | no Water ] -> [ Water WaterCoverUp | ]



(water depth effect)
[ WaterDepth ] -> []


[ Spirit Water ] -> [ Spirit Water WaterDepthneg1 ]
[ Spirit | Water ] -> [Spirit | Water WaterDepthneg1 ]

[ SpiritLightMarker Water ] -> [ Water WaterDepthneg1 ] (lighten spot the spirit was previously)
[ SpiritLightMarker | Water ] -> [ SpiritLightMarker | Water WaterDepthneg1]
[ SpiritLightMarker ] -> []
[ Spirit ] -> [ Spirit SpiritLightMarker ] 

[ Water no WaterDepth | no Wall WaterDepthneg1 ] -> [ Water WaterDepth0 | WaterDepthneg1 ]


[ManualWaterDepth0 no WaterDepth] -> [ManualWaterDepth0 WaterDepth0]

(like it with dark at bottom, light at top) (manually place some water tiles)
down [ Water no WaterDepth no ManualWaterDepth4 | no Water no wall] -> [ Water WaterDepth0 | ]
[ Water no WaterDepth | no Wall WaterDepth0 ] -> [ Water WaterDepth1 | WaterDepth0 ]
[ Water no WaterDepth | no Wall WaterDepth1 ] -> [ Water WaterDepth2 | WaterDepth1 ]
(down [ Water no WaterDepth | no Water] -> [ Water WaterDepth2 | ])
[ Water no WaterDepth | no Wall WaterDepth2 ] -> [ Water WaterDepth3 | WaterDepth2 ]
[ Water no WaterDepth | no Wall WaterDepth3 ] -> [ Water WaterDepth4 | WaterDepth3 ]
up [ Water no WaterDepth | no Water ] -> [ Water WaterDepth4 | ]
[ManualWaterDepth4 no WaterDepth] -> [ManualWaterDepth4 WaterDepth4]
[ Water no WaterDepth | no Wall WaterDepth4 ] -> [ Water WaterDepth5 | WaterDepth4 ]
[ Water no WaterDepth | no Wall WaterDepth5 ] -> [ Water WaterDepth6 | WaterDepth5 ]
[ Water no WaterDepth | no Wall WaterDepth6 ] -> [ Water WaterDepth7 | WaterDepth6 ]
[ Water no WaterDepth ] -> [ Water WaterDepth8 ]









(crate sinking animation)
[ StoneCrateSink9 ] -> []
[ StoneCrateSink8 ] -> [ StoneCrateSink9 ] 
[ StoneCrateSink7 ] -> [ StoneCrateSink8 ] 
[ StoneCrateSink6 ] -> [ StoneCrateSink7 ] 
[ StoneCrateSink5 ] -> [ StoneCrateSink6 ] 
[ StoneCrateSink4 ] -> [ StoneCrateSink5 ] 
[ StoneCrateSink3 ] -> [ StoneCrateSink4 ] 
[ StoneCrateSink2 ] -> [ StoneCrateSink3 ] 
[ StoneCrateSink1 ] -> [ StoneCrateSink2 ]
[ StoneCrateSink0 ] -> [ StoneCrateSink1 ]

[ StoneCrateSinkCover9 ] -> []
[ StoneCrateSinkCover8 ] -> [ StoneCrateSinkCover9 ] 
[ StoneCrateSinkCover7 ] -> [ StoneCrateSinkCover8 ] 
[ StoneCrateSinkCover6 ] -> [ StoneCrateSinkCover7 ] 
[ StoneCrateSinkCover5 ] -> [ StoneCrateSinkCover6 ] 
[ StoneCrateSinkCover4 ] -> [ StoneCrateSinkCover5 ] 
[ StoneCrateSinkCover3 ] -> [ StoneCrateSinkCover4 ] 
[ StoneCrateSinkCover2 ] -> [ StoneCrateSinkCover3 ] 
[ StoneCrateSinkCover1 ] -> [ StoneCrateSinkCover2 ]
[ StoneCrateSinkCover0 ] -> [ StoneCrateSinkCover1 ]



(delete the spirit if it gets to the edge of the screen)

[ Spirit ] -> [ Spirit SpiritLeftScreen ]
up [ Spirit SpiritLeftScreen | ] -> [ Spirit | ]
[ SpiritLeftScreen Spirit ] -> [ SpiritLeftScreen ] sfx2

[ Spirit ] -> [ Spirit SpiritLeftScreen ]
down [ Spirit SpiritLeftScreen | ] -> [ Spirit | ]
[ SpiritLeftScreen Spirit ] -> [ SpiritLeftScreen ] sfx2

[ Spirit ] -> [ Spirit SpiritLeftScreen ]
right [ Spirit SpiritLeftScreen | ] -> [ Spirit | ]
[ SpiritLeftScreen Spirit ] -> [ SpiritLeftScreen ] sfx2

[ Spirit ] -> [ Spirit SpiritLeftScreen ]
left [ Spirit SpiritLeftScreen | ] -> [ Spirit | ]
[ SpiritLeftScreen Spirit ] -> [ SpiritLeftScreen ] sfx2
 
(collision effects)
[TempSpiritAlreadyCollided no Spirit] -> []
up 		[ Player | ... | Spirit no TempSpiritAlreadyCollided no bush | Wall ] -> [ Player | ... | SpiritHitsU TempSpiritAlreadyCollided | Wall ] sfx3
down 	[ Player | ... | Spirit no TempSpiritAlreadyCollided no bush | Wall ] -> [ Player | ... | SpiritHitsD TempSpiritAlreadyCollided | Wall ] sfx3
right 	[ Player | ... | Spirit no TempSpiritAlreadyCollided no bush | Wall ] -> [ Player | ... | SpiritHitsR TempSpiritAlreadyCollided | Wall ] sfx3
left 	[ Player | ... | Spirit no TempSpiritAlreadyCollided no bush | Wall ] -> [ Player | ... | SpiritHitsL TempSpiritAlreadyCollided | Wall ] sfx3

up 		[ Player | ... | Spirit no TempSpiritAlreadyCollided no bush | Crate ] -> [ Player | ... | SpiritHitsU TempSpiritAlreadyCollided | Crate ] sfx4 sfx5
down 	[ Player | ... | Spirit no TempSpiritAlreadyCollided no bush | Crate ] -> [ Player | ... | SpiritHitsD TempSpiritAlreadyCollided | Crate ] sfx4 sfx5
right 	[ Player | ... | Spirit no TempSpiritAlreadyCollided no bush | Crate ] -> [ Player | ... | SpiritHitsR TempSpiritAlreadyCollided | Crate ] sfx4 sfx5
left 	[ Player | ... | Spirit no TempSpiritAlreadyCollided no bush | Crate ] -> [ Player | ... | SpiritHitsL TempSpiritAlreadyCollided | Crate ] sfx4 sfx5

([ stationary Player ][ SpiritHitsU Timer5 ] -> [ stationary Player ][ SpiritStationary ]
[ stationary Player ][ SpiritHitsU Timer4 ] -> [ stationary Player ][ SpiritHitsU Timer5 ]
[ stationary Player ][ SpiritHitsU Timer3 ] -> [ stationary Player ][ SpiritHitsU Timer4 ]
[ stationary Player ][ SpiritHits Timer2 ] -> [ stationary Player ][ SpiritStationary ])
[ stationary Player ][ SpiritHits Timer1 ] -> [ stationary Player ][ SpiritStationary ]
[ stationary Player ][ SpiritHits Timer0 ] -> [ stationary Player ][ SpiritHits Timer1 ]
[ stationary Player ][ SpiritHits no Timer] -> [ stationary Player ][ SpiritHits Timer0 ]


(movement sprites)
up 		[ > Spirit no bush | no wall no Crate ] -> [ > SpiritMovesU | ]
down 	[ > Spirit no bush | no wall no Crate ] -> [ > SpiritMovesD | ]
right 	[ > Spirit no bush | no wall no Crate ] -> [ > SpiritMovesR | ]
left 	[ > Spirit no bush | no wall no Crate ] -> [ > SpiritMovesL | ]

[ stationary spirit no TempSpiritAlreadyCollided ] -> [ stationary SpiritStationary ]
late [ SpiritMoves Bush ] -> [ SpiritStationary Bush ] sfx6


(--------------------------------------)



[stationary Spirit Target][ moving Player ] -> [stationary Spirit Target][ stationary Player ] (don't let player move during bounce if they're going to win)
[stationary SpiritStationary Target] -> win sfx7

==============
WINCONDITIONS
==============

=======
LEVELS
=======
(
message Every year, biologists ___ the rare ____ . But they have to catch them first.

messages can be about the strange behaviours/properties of the species.

name idea: Re-homing Pigeons. Lore is that [after solar flares], species of pigeon has 
lost their sense of direction and you are there to guide them to their nests (by frightening them) 

todo: make bushes more satisfying?
make top edges look nicer?
make player more distinct?

introduce bushes before crates?
)

~~~~~~~~~~
~~~#...#.~
~~.#o....~
~........~
~........~
~~#.s...~~
~~......#~
~.....p.#~
~~.###...~
~~~~~~~~~~

~~~~~~~~~~~
~~~~~~~..~~
~~~###...~~
~~~....#.~~
~~...p....~
~~........~
~~..s.o#..~
~##...##.~~
~~.......~~
~~~.....~~~
~~~~~~~~~~~


~~~~~~~~~~~~
~~~~####~#~~
~####..o~~~~
~L........~~
~~.#..s...~~
~~......p.~~
~~~##...###~
~~~~#.#.#~~~
~~#~~...~~~~
~~~~~~~~~~~~


~~~~~~~~~~~~
~~~....##~~~
~~#.#.....~~
~#...~.#...~
~#.#.~~.s..~
~~..~~~..p.~
~~~..~~~.##~
~~~~.~~~o#~~
~~~~#~#~~#~~
~~~~~~#~~~~~
~~~~~~~~~~~~


~~~~~~~~~~~~~~~
~~~#~..~..~~~~~
~.~#.......~~~~
~~~#........~~~
~~.#o.*s..#.~~~
~~~.......#..~~
~.~#.p.......~~
~.~~~......~~~~
~~~~~~#.~~~~#~~
~~~~~~~~~~~~~~~

~~~~~~~~~~~~~
~~~~~..~~~~~~
~~~~.....###~
~~###.....o#~
~~#........#~
~.#..s......~
~...........~
~..*....##.~~
~~....~.##.~~
~~.p.~~....~~
~~~~~~~~~~~~~

~~~~~~~~~~#~~~
~#~~~~~~~~~#~~
~~~~~....~~~~~
~~~...*..~~#~~
~~....o...~~#~
~~..p.*...~~#~
~~.......~~~#~
#~....s..~~#~~
~~~......~~#~~
~#~~~~..~~~~#~
~#~~~~~~~~#~~~

.....~~~~~~~~
#########~~~~
#.......#.~~~
#.*lll.o#...~
#..~~~......~
#..~~~..s...~
#..~~~......~
#.*ddd.p#...~
#.......#..~~
#########.~~~

~~~~~~~~~~~~~~~
~~...~~~~~..#~~
~~.s.~~~~~..#~~
~~...~~~~~..#~~
~~...~~~~~..#~~
~~...#...#..#~~
~~.o.*.p.*..#~~
~~..........#~~
~~~~~~~~~~~~~~~ (send back the way it came with crate)

.b...~~~~~~~~
##.######~~~~
#.......#.~~~
#.*lll.o....~
#..~~~....#.~
#..~~~..s...~
#..~~~.....#~
#.*ddd.p#...~
#.......#b.~~
#########.~~~


~~~~~~~~~~~~~
~~~~~....~~~~
~~.......~~~~
~#*##*#...b~~
~..*..#~...#~
~p.#..#~~..#~
~b.#.~~~w*.#~
~~.#~~~#DD..~
~~~#~~~#*...~
~~~~~#~bo.#~~
~~~~~~~~#~~~~
~~~~~~~~~~~~~  (level introducing destroying crates)

(two solutions)

~~~~~~~~~~~~~
~~~~~....~~~~
~~.......~~~~
~#*##*#...b~~
~..*..#~...#~
~p.#..#~~..#~
~b.#.~~~w*.#~
~~.#~~~#dd..~
~~~#~#o.*...~
~~~~~~~~..#~~
~~~~~~#~~b~~~
~~~~~~~~~~~~~





####~~~######
##~~~~~~~...#
#...**.*....#
#po*.**...s.#
#..*..*.*...#
##~~~~~~~...#
####~~~######  (destroy crates that are in the way)



(levels introducing bushes as limited use cover)

~~~~~~~~~~~~~~~~
~~~~#~~~~~~~~~~~
~~#~~~.b..~~~~~~
~~~~#...b.b~#~~~
~~~~##b##....~~~
~~~......~.#b.~~
~~#.#os.b~~D..~~
~~#.....b~~.#.~~
~~##.....~~..b~~
~~~~..##~~~p.~~~
~~~~~#~~~~~~~~~~
~~~~~~~~~~~~~~~~

~~~~~~~~~~~~~~~
~~~~#~~#~~~~~~~
~~~...~#~~~~~~~
~~..#~~~~~~b~~~
~~b.#~#~~##.#~~
~~....#~..b..~~
~b.#..........~
~..b....s.....~
~..........#..~
~..p.bo###...~~
~~b..####b.~~~~
~~~...~~~~~~~~~
~~~~~~~~~~~~~~~


(levels introducing bushes as catching spirit)


~~~~~~~~~~~~~~
~~~~~...~~~~~~
~~....b.#~##~~
~~.b.........~
~............~
~#b...s...#..~
~#.bo#.......~
~#..b~~b...b~~
~~.p.#~~~...~~
~~~~##~~#~~~~~
~~~~~~~~~~~~~~ (makes it look like bushes will stop spirit like walls) (maybe add an animation for entering bush for the first time?)
(level ends too soon (graphics bug))


~~~~~~~~~~~
~#~...~...~
~~~.L.~.~.~
~..b#b.h..~
~.~.~.~.~.~
~p.b.b.b..~
~.~.~.L.~.~
~..j.b#b..~
~.~.~.~.~~~
~...~...~#~
~~~~~~~~~~~

~~~~~~~~~~~~~~~~~
~~~~~~~~~......~~
~~....~....~~...~
~...b.~~.b..~b..~
~~~....~~~~.~~~~~
~~........~.~..~~
~~~.b..b.w~..b..~
~~~~~~....~~...~~
~~~..~.........~~
~...b..o.b.p.b~~~
~~.....#....~~~~~
~~~~~~~~~~~~~~~~~ (can't walk adjacent to bush w/ spirit (must be on correct side of it). Use Bush as Cover.)

(
~~~~~~~~~
~~.....~~
~.b.b#h.~
~..b.j..~
~.#.b.b.~
~..b.b..~
~.b.b.b.~
~~...p.~~
~~~~~~~~~
)

(level where you must leave at least one bush per col as cover?)

(
~~~~~~~~~
~.#.....~
~...~bo#~
~.s.~...~
~#..~...~
~..p~#..~
~...~..#~
~.#.~...~
~~~~~~~~~ (new path opens because bush is used?)

~~~~~~~~~~~~
~.#......~.~
~...~b..*~o#
~.s.~....~.~
~#.p~....~.~
~...~###.~.~
~~~~~....~~~
~~~~~~~~~~~~)


(intentionally break bushes)

~~~~~~~~~~~~
~~~.#.~~~~~~
~~......o#~~
~~.#b~.....~
~#.~~~~....~
~...~~~~...~
~~..s.~~b.#~
~~p.......~~
~~~.b...#~~~
~~~~~~~~~~~~ (bush you can't break and run until moving the spirit)(forces you to do a circuit)


LLLLLLLLL~~~~~~~~~~
#o~b~.b.LL~~~~~~~~~
L.....L.LL~~~~~~~~~
#.#L~~~b~.b.h.LbLb.
#.#~......L...L.L..
#..b...##.L.p...L..
#####..##.#........  (intentionally breaking bushes, and progress gated by bushes that must be used constructively)


~~~~~~~~DDD~~
~####~#~...#~
~#o..~b~w~b#~
~#.~...L~~.#~
~~.~~~~Lb...~
~~.~~~~~~~L.~
~#.#b.b..bL.~
~#.~~.~.#~~.~
~#...p......~
~~~~~~~~~~~~~ (intentionally break so it won't block line of sight)(break when in view of spirit / break and go back)


(
~~~~~~~~~~~~~~~
~~~~#~~~~~~~~~~
~~#~##~~~~~.~~~
~~#~..~~~b~o#~~
~~~~.bb.~b...~~
~#~~...~~..p..~
~~~~~..~......~
~.#~.#b..s.#.~~
~..###...b...~~
~~.##~~..#...~~
~~#.#~~#~~#.~~~
~~~~~~#~~~~~~~~
~~~~~~~~~~~~~~~ (breaking bushes)(focus on double bushes) (two bushes are red herrings. Is this bad?)
)




(hiding in bush adjacent to spirit in bush, and pushing spirit free)


~~~~~~~~~~~~~~~~~~~
~~~~~~~..#.~~~###~~
~~...b...#..b..o#~~
~xs..b.~b...b.~.~~~
~##..b....#.b.....~
~#..~~~..~#.....#.~
~~#~~~~~#~#~~##...~
~~~~~~~~~~~~~~~~~~~ 

(
~~~~~~~~~~~~~~~~~~~~
~#~~~~~~~~~~~~#~#~~~
~~~..b......b.#~~~~~
~xs..b.~b...b.#o...~
~##..b....#.b....b.~
~~~~~~~~~~#......b.~
~~~~~~~~#~#~~~~~~~~~
~~~~~~~~~~~~~~~~~~~~

~~~~~~~~~~~~~~~~
~~~~~~~#..~~~~~~
~~...b......b.~~
~xs..b.~b...b.~~
~##..b....#...#~
~#..~~~..~#..o#~
~~#~~~~~#~#~~##~
~~~~~~~~~~~~~~~~

~~~~~~~~~~~~~~~~~~
~~~~~~~..#.~~~###~
~~...b...#..b..o#~
~xs..b.~b...b.~.~~
~##..b....#.b...~~
~#..~~~..~#.....#~
~~#~~~~~#~#~~##~~~
~~~~~~~~~~~~~~~~~~ 
)

~~~~~~~~~~~~~~~
~~~~~~~.#....~~
~~~#.....p.#..~
~.bhb..bbbbbbo~
~~.....#......~
~~~~#.....#...~
~#~~~~~.#...~~~
~~~~~#~~~~~~~#~
~~~~~~~~~~~~~~~ (shows how you can win from pushing spirits)


~~~~~~~~~~~~~~
~~~##~......~~
~#~.....~~#.~~
~~~.~b.#~~~..~
~~..~b.bbh~..~
~#...b..~...~~
~~...b~....~~~
~##o.....p.~#~
~~###.~#~##~~~
~~~~~~~~~~~~~~


~~~~~~~~~~~~~~~
~~#~~~..~~~~#~~
~~~~.bb.~..~#~~
~~.......b.~~~~
~#.b.b...b..b.~
~..b.b.p....h.~
~.............~
~~##..obb..#~~~
~~~~~~####~~~~~
~~~~~~~~~~~~~~~ (double bushes and pushing spirit free)


(line of bushes)
(
~~~~~~~~~~~~~
~~~.#.....~~~
~.....p.#...~
~.hbbbbbbbo.~
~..#.......#~
~.....#.....~
~~~~#....~~~~
~~#~~~~#~~~~~
~~~~~~~~~~~~~
)



(pushing spirit in field of bushes)
(
~~~~~~~~~
~.bbbbb.~
~p~hjbb.~
~~bb~b~~~
~.bbbbb.~
~...~...~
~~~~~~~~~)


(intentionally breaking + hiding in bush next to spirit)
~~~~~~~~~~~
~~~.b.~...~
~.bb~b..j.~
~.~.b.~...~
~...~~~~~~~
~~~b....b.~
~..b.b~bs.~
~b~b.~~.L.~
~.~~~~~.#.~
~...p.....~
~~~~~~~~~~~




(crates and bushes)

~~~~~~~~~~~~~
~~~b..~~~~~~~
~###.p.~~#~#~
~~........o#~
~~.*.b~~b..~~
~#........##~
~#~.....s.~~~
~~~....#~##~~
~~~~~~~~~~~~~


~~~~~~~~~~~~~
~~.....~~~#~~
~.h.*.b..~~~~
~.........~#~
~.....b.b.~~~
~.....~~..~~~
~~~.p.~~bo#~~
~#~...~~~~~#~
~~~~~~~~~~~~~




(place crate next to spirit in bush so you can walk past without breaking it)
~~~~~~~~~~~~~~~~~
~~~~~~#~~~#~~~~~~
~~#~~~~~#~~~#~~~~
~~~~~~~~~~~~~~~~~
~...............~
~.b.p.h.b.*..bb.~
~.......#.#.o...~
~~#~~~#~#~#~#~~~~
~~~~~~~~~~~~~~~~~



(push crate through bush)

(
~~~~~~~~~~~~~
~~~.......~~~
~~.o.....p.~~
~~....#....~~
~~..b.s.b..~~
~~.........~~
~~..*...*..~~
~~~.......~~~
~~~~~~~~~~~~~
~~~~~~~~~~~~~)

(
~~~~~~~~~~~~~~
~~~....~~~~u~~
~~#..~b..~~u~~
~~~#.~....~~~~
~~~#.~~~..~~#~
~#~~b~w~b.~o#~
~~#..~~~..~.#~
~#........~~~~
~~.p.~.*...~~~
~~~..~b....~~~
~~##~~...~~~#~
~~~~~~~~~~~~~~ (ok))

~~~~~~~~~~~~~~
~~~....~~~~u~~
~~#..~b...~u~~
~~~#.~....~~~~
~~~#.~~~..~~#~
~#~~b~w~b..o#~
~~#..~~....~#~
~#.#.*b.#..~~~
~~b..~.....#~~
~~~.p~b....~~~
~~##~~...~~~#~
~~~~~~~~~~~~~~ (good)(maybe too complicated to introduce concept?)


(
~~~~~~~~~~~~~
~....~~~~~~~~
~....b......~
~...........~
~b...s....b~~
~....~......~
~.*..b...*..~
~....~.p....~
~~~~~~~~~~~~~ (create permanent pathway with crate?)

~~~~~~~~~~~
~~~...~~~~~
~~..#.....~
~~..b..*..~
~~........~
~~..s.....~
~~..~.p...~
~~..b...*.~
~~..~~~...~
~~.ob.~~~~~
~~~.#.~~~~~
~~~...~~~~~
~~~~~~~~~~~ (doesn't work)


~~~~~~~~~~~~~
~~.........~~
~~.*.~~~...~~
~~.........~~
~~...~~~...~~
~~.o.~w~...~~
~~...~~~...~~
~~.........~~
~~.....p.*.~~
~~.........~~
~~~~~~~~~~~~~

~~~~~~~~~~~~~~~
~~~....b....~~~
~#~...~~~.o.~~~
~~~....b...~~~~
~#~...~~~...~~~
~~~...~w~b~b~#~
~~~...~~~...~~~
~...*.~...*.~#~
~.....~.p...~~~
~~~~~~~~~~~~~~~ (maybe something - impossible?)
)



(Push crate in bush to get it past spirit)

~~~~~~~~~~~~~~
~~~~~~~####L~~
~~...#~o..L~~~
~~........~~~~
~##b..s..b##~~
~~...##~#..~~~
~~.........~~~
~~..*.p.b..##~
~#####~~#..~~~
~~~~~~~~~~~~~~

~~~~~~~~~~
~.....###~
~........~
~.s.b~b~b~
~#~......~
~#o.p.*..~
~#~......~
~~~~~~~~~~ (ok) (combine with other level?) 



(level where you must get both crates to the same place?)


(level where you must push a crate through bushes and conserve them?)



(push crate in bush so that spirit can hit it)

~~~~~~~~~~~~
~~~#.......~
~#~#.p...%.~
~~~#.....w.~
~~~~~L.L~~.~
~#o.bLbL...~
~#.~.L.#.##~
~#.~.....##~
~~~~~~~~~~~~ 

~~~~~~~~~~~~
~~~.....b..~
~~~..%..b..~
~#~..%.~s~.~
~~~p.~.~.~.~
~~~~~~.~~~.~
~#o.blbl...~
~#.~.l.#.##~
~#.~.....##~
~~~~~~~~~~~~


(crate and double bushes)


~~~~~~~~~~~~~~~
~##~..........~
~.....bb...~~.~
~..p.....~.bb.~
~.....~~......~
~.~.*.......s.~
L.o........~..~
~~~~~~~~~~#~~~~
~#~~##~~#~~~#~~
~~~~~~~~~~~~~~~

(Push crate in bush to get it past spirit in bush)


~~~~~~~~~~~~~~~~~~
~.....#...~#.~.~#~
~.ub......~#o.bl.~
~..bb~~~~.~#..b#.~
~.%hb~#...~.b....~
~..b.~#b.b..b%.##~
~.p..~#~#~~....##~
~~~~~~~~~~~~~~~~~~



(End Last level with a cinematic)

~~~~~~~~~~~~
~~~....###~~
~~~.#.p..#~~
~~#.....ou~c
~~~..#.s.#~~
~~~~.....#~~
~~~~~...##~~
~~~~~~~~~~~~


~~~~~~~~~~~~~~~~~~
~~~#~~~~#~~~~~~#~~
~~~~~~~~~~~~#~~~~~
Fw~~~~~~~u~~~~~~~C
~~~~#~~~~~~~~~~~~~
~~~~~~#~~~#~~~#~~~
~~~~~~~~~~~~~~~~~~

.b...~~~~~~~~
##.##.###~~~~
#.....b.#.~~~
#.*lll..b.b.~
fs.~~~......c
#..~~~b...b.~
.b.~~~.b..~#~
#.*ddd..#...~
#.....b.#b~~~
##.b#####.~~~

~~~~~~~~~~~~~~##~~
~#~~~~~~~~#~~###~#
~~~~~#~~~~~~##~##~
cfw~~~~~~~~~uuuuI~
~~~~~~~~~~~~######
~~~~#~~~~~~#~#~##~
~~~~~~~~~~~~~~~~~~

.b...~~~~~~~~
##.##.###~~~~
#.....b.#.~~~
#.*lll..b.b.~
c..~~~.....wf
#..~~~b...b.~
.b.~~~.b..~#~
#.*ddd..#...~
#.....b.#b~~~
##.b#####.~~~

~~~~~~~~~~~~~~~~~~
~~~#~~~~#~~~~~~#~~
~~~~~~~~~~~~#~~~~~
C~~~~~~~~~~~~~~~wF
~~~~#~~~~~~~~~~~~~
~~~~~~#~~~#~~~#~~~
~~~~~~~~~~~~~~~~~~

~~~~~~~~~~~~
~~~....###~~
~~~.#....#~~
c~u.{...o~wf
~~~..#...#~~
~~~~.....#~~
~~~~~...##~~
~~~~~~~~~~~~

~~~~~~~~~~~~~~~~~~
~~~~~~~~~~~~~~~~~~
~~~~~~~~~~~~~~~~~~
c~u~~~u~~~u~~~~&wf
~~~~~~~~~~~~~~~~~~
~~~~~~~~~~~~~~~~~~
~~~~~~~~~~~~~~~~~~

~~~~~~~~~~~~~~~~~~
~~~~~~~~~~~~~~~~~~
~~~~~~~~~~~~~~~~~~
c~~~u~~~~~~u~~~&wf
~~~~~~~~~~~~~~~~~~
~~~~~~~~~~~~~~~~~~
~~~~~~~~~~~~~~~~~~

(
~~~~~~~~~~~~~~~~~~~
~~~~~~~#~~~#~~~~~~~
~~~#~~~~~#~~~#~~~~~
~~~~~~~~~~~~~~~~~~~
~~...............~~
~~.b...b.b.*..bb{wf
~~.......#.#.o...~~
~~~#~~~#~#~#~#~~~~~
~~~~~~~~~~~~~~~~~~~ (get caught in bush? or propel along with crate?))

~~~~~~~~~~~~~~~~~~~
~~~~~~~#~~~#~~~~~~~
~~~#~~~~~#~~~#~~~~~
~~~~~~~~~~~~~~~~~~~
~~...............~~
c~.'...'.+.*..';{wf
~~.......#.#.o...~~
~~~#~~~#~#~#~#~~~~~
~~~~~~~~~~~~~~~~~~~


message The End. Thanks for playing!







(
(misclevels)


~~~~~~~~~~~~~~~
~~~...~~~....~~
~~..b...~b.b..~
~~.b..b......b~
~#..#o....s..#~
~#.........#~~~
~....p.b.b..~~~
~~b#.........~~
~~~#..b...b.#~~
~~~##.......~~~
~~~~~~~#...~~~~
~~~~~~~~~~~~~~~ 


~~~~~~~~~
~~...*.#~
~~*..*.#~
~#*~.#~~~
~..*so.*~
~p#.....~
~.*.~**.~
~~##~~##~
~~~~~~~~~

~~~~~~~~~~~
~~~~#~~~~~~
~~...~...~~
~~.......~~
~~..#..~~~~
~...#..s~~~
~....o....~
~##*#*#.#.~
~~..#.....~
~~....p~~~~
~~~.#~~~~~~
~~~~~~~~~~~ (meh?) (fill gaps)

~~~~~~~~~~~~~~~~~~~
~~.#....~~~~~~~~~~~
~.....p.#~~#....~~~
~..~~~...~~.....#~~
~...~~~..~~..~#...~
~.b.~~~.~~...~~.o#~
~....~..~~...~~...~
~.s.#~...~..~#~...~
~~..~~......#~~#..~
~~...~~#....~~...~~
~~~~~~~~~~~~~~~~~~~ 

~~~~~~~~~~~~
~~~~~~...~~~
~~~......~.~
~#~*##*#.~~~
~~..*..#..~~
~#p.#..#..~~
~#..#~~.s..~
~~#~~......~
~#o.~~....~~
~####~~#...~
~~~~~~~~~~~~

~~~~~~~~~~~~~
~~~##~~~~###~
~###o.b.p..#~
~#.....~...#~
~#bb..b.bhb#~
~#bb...~~~##~
~##~~~~~~~~~~
~~~~~~~~~~~~~

#########
~o~~~~~p~
~.bbbbb.~
~~.~b~.~~
~~.~h~.~~
~.bbbbb.~
~.~~~~~.~
#########

~~~~~~~~~~~~~
~~~...###~~~~
~~.p.......#~
~~..b~*~b..~~
~#.#..s~..##~
~~~#o.....~~~
~~#~~..#~##~~
~~~~~~~~~~~~~

~~~~~~~~~~~~~~
~~~~~~...~~~~~
~~.......~#~~~
~~..~~.b.~~~#~
~#.........o#~
~#..bb.s.b##~~
~#~#..~....~~~
~~~~~.##...~#~
~~~#~....*.~~~
~~~~~.p....~~~
~~~~~~~~~~~~~~




~~~~~~~~~~~~~~~~~
~~~~~...~~~~~~~~~
~~~~~....~~~..~~~
~~~~....s~....~~~
~~~~.*...~~~~..~~
~~~~.#.*....~~..~
~#o.........~~.~~
~~~~~.~~....~~.~~
~~~~~p....~~~..~~
~~~~~~~~.~~...~~~
~~~~~~~~..~..~~~~
~~~~~~~~....~~~~~
~~~~~~~~~~~~~~~~~





(misc non-levels)


~~~~~~~~~~~
~~~~~~####~
~~~~~~#...~
~####~#*..~
~...#~#...~
~....s....~
~.........~
~..*.#....~
~.........~
~.p.......~
~~~~~~~~~~~
~~~~~~~~~~~ (level where you recover box)





~~~~~~~~~~~~~
~~~~~~###~~~~
~~~~~#.o.~~~~
~~~.......~~~
~~~.#...s.~~~
~~........#~~
~~.#.......~~
~~......#..~~
~~#.p......~~
~~~......#.~~
~~~~~~~~~~~~~
~~~~~~~~~~~~~

~~~~~~~~~~~~~~~
~~~~~.#....~~~~
~~~.....#.....~
~~........#...~
~..b........#.~
~.....s....b..~
~.b.p......#..~
~......b.#.~~~~
~~..b..#...~~~~
~~~...~~~~~~~~~
~~~~~~~~~~~~~~~


~~~~~~~~~~~~~~~
~b.b.b~#~b.b.b~
~.~.~.~o~.~.~.~
~b.b.h.b.b.b.b~
~.~.~.~~~.~.~.~
~b.b.b.b.b.b.p~
~.~.~.~~~.~.~.~
~b.b.b~~~b.b.b~
~~~~~~~~~~~~~~~


...............
.~.b.~.b.~.b.~.
b.b~b.b~b.b~b.b
~.~~~.~~~.~~~.~
~b.b.b~w~b.b.b~
~~~.~~~~~~~.~~~
~~~p...b....~~~
~~~~~~~~~~~~~~~

~~~~~~~~~~~~~
~...........~
~...b....b..~
~.b....b....~
~....b....b.~
~..b....b...~
~.....b.....~
~.p.b....b..~
~...........~
~~~~~~~~~~~~~


)