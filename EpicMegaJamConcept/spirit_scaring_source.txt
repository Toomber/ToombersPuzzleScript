title EpicJamGame
author Toombler
homepage www.puzzlescript.net

again_interval 0.05
realtime_interval 0.1

run_rules_on_level_start

(verbose_logging)

========
OBJECTS
========

Background
#CCDCEA #99C6E2  #ffaf47 #ffbf69
11011
01110
11001
11100
00111

WaterCoverUp
#7fb0d7 #4089c4 (#96bede #67a1d0) (#95e6de #2ec2b4)
00000
11111
.....
.....
.....

Target
DarkBlue
.....
.000.
.0.0.
.000.
.....

Wall
#3a3c3a #505451 #6a6e6b #8c908c
20203
22302
02312
03010
20010


(BROWN DARKBROWN
00010
11111
01000
11111
00010)



Player
#383430 #A09D95 #6D6C69 #574236
.000.
.111.
33333
02220
.0.0.

StoneCrate
#a05f75 #a35c68 #b14e6d #94555c #da737b #6d4b78
02322
3...2
3...2
3...1
33331

StoneCrateSink0
#966179 #995E6E #A65172 #8B5863
02322
3...2
3...2
3...1
33331

StoneCrateSink1
#8C637E #8F6073 #9A5577 #835A69
02322
3...2
3...2
3...1
33331

StoneCrateSink2
#836482 #856279 #8F587D #7A5D70
02322
3...2
3...2
3...1
33331

StoneCrateSink3
#796687 #7B647F #845B82 #716077
02322
3...2
3...2
3...1
33331

StoneCrateSink4
#6F688B #716684 #785F87 #68637E
02322
3...2
3...2
3...1
33331

StoneCrateSink5
#656A90 #66698A #6D628C #606584
02322
3...2
3...2
3...1
33331

StoneCrateSink6
#5B6C94 #5C6B8F #616691 #57688B
02322
3...2
3...2
3...1
33331

StoneCrateSink7
#516E99 #526D95 #566996 #4E6B92
02322
3...2
3...2
3...1
33331

StoneCrateSink8
#486F9D #486F9B #4B6C9C #456E99
02322
3...2
3...2
3...1
33331

StoneCrateSink9
#3E71A2 #3E71A0 #3F70A1 #3D709F
02322
3...2
3...2
3...1
33331

SpiritStationary
Purple
.....
.000.
.000.
.000.
.....

SpiritHitsU
Purple
00000
00000
.....
.....
.....

SpiritHitsD
Purple
.....
.....
.....
00000
00000

SpiritHitsR
Purple
...00
...00
...00
...00
...00

SpiritHitsL
Purple
00...
00...
00...
00...
00...

SpiritMovesU
Purple
.000.
.000.
.000.
.000.
.....

SpiritMovesD
Purple
.....
.000.
.000.
.000.
.000.


SpiritMovesR
Purple
.....
.0000
.0000
.0000
.....

SpiritMovesL
Purple
.....
0000.
0000.
0000.
.....


(glass is solid but not opaque)

GlassWall
lightblue
(0.1.0
00.1.
.00.1
1.00.
.1.00)

GlassCrate
lightblue
00000
0...0
0...0
0...0
00000

(Bushes are opaque but not solid)
(idea: Bush + glass block = normal block?

Spirit hides in bushes
Bushes are destroyed when used by you or the spirit
Spirit leaves whenever you are one block away?

Maybe the spirit revives dead bushes it passes over? - would have to be when it stops moving. Maybe a different kind of bush?

Can you push the spirit with a crate? Nah

Maybe the main character is a fan? Has a Leaf blower?


idea: make it so that the spirit leaving the screen at a sand block opens a secret?
)

Bush
#4b916c #3e785a #579a65 #BD4242 darkgreen green
.....
.0...
021.0
10212
10102

DestroyedBush
#4b916c #3e785a #BD4242 green
.....
.....
.....
.....
0.01.

TempVacatedBush
black

TempSpiritAlreadyCollided
transparent

SpiritLeftScreen
transparent

Water 
#3473a6 (#3B83BD) (#27A599)

BlockingU
transparent

BlockingD
transparent

BlockingR
transparent

BlockingL
transparent

Timer0
transparent

Timer1
transparent

Timer2
transparent

Timer3
transparent

Timer4
transparent

Timer5
transparent



=======
LEGEND
=======

. = Background
# = Wall
P = Player
* = StoneCrate
@ = StoneCrate and Target
% = StoneCrate and Bush
O = Target
S = SpiritStationary and TempSpiritAlreadyCollided
~ = Water
W = SpiritStationary and Water and TempSpiritAlreadyCollided
B = Bush
H = Bush and SpiritStationary and TempSpiritAlreadyCollided
J = Bush and Target
X = Player and Bush

G = GlassCrate
T = GlassWall


Blocking = BlockingU or BlockingD or BlockingR or BlockingL
SpiritHits = SpiritHitsU or SpiritHitsD or SpiritHitsR or SpiritHitsL
SpiritMoves = SpiritMovesU or SpiritMovesD or SpiritMovesR or SpiritMovesL
Spirit = SpiritStationary or SpiritHits or SpiritMoves
Crate = StoneCrate or GlassCrate
Opaque = StoneCrate or Wall or Bush

Timer = Timer0 or Timer1 or Timer2 or Timer3 or Timer4 or Timer5 

StoneCrateSink = StoneCrateSink0 or StoneCrateSink1 or StoneCrateSink2 or StoneCrateSink3 or StoneCrateSink4 or StoneCrateSink5 or StoneCrateSink6 or StoneCrateSink7 or StoneCrateSink8 or StoneCrateSink9

=======
SOUNDS
=======

Crate MOVE 36772507

Crate destroy 45416100 (33195900) (58667903)

Endlevel 96976903

Spirit move 50223502 (39212302)
sfx2 39212302 (sound for when spirit has left screen)

Player move 74291507

sfx0 84286303 (96059500 31288108)  (7389303) (kick spirit out of bush)

sfx1 49801907 (26041300) (64859107)  () (destroy bush)

sfx3 2447107 (spirit collides with wall)
sfx4 33912304 (71683100) (76311307) (spirit collides with crate)
sfx5 13888100 (63113700) (spirit collides with crate 2)

================
COLLISIONLAYERS
================

Background

Water

StoneCrateSink

WaterCoverUp

Target
DestroyedBush
Player, Wall, Crate, Spirit, GlassWall, GlassCrate
Bush,

Blocking
TempVacatedBush
TempSpiritAlreadyCollided
SpiritLeftScreen
Timer



======
RULES
======


[ > Player | Water ] -> [ Player | Water ] (don't let player enter water)
[ >  Player | Bush Crate ] -> [  Player | Bush TempVacatedBush > Crate ] sfx1 (push crate that's inside a bush, Akurra style. Should this destroy the bush?)
[ >  Player | Crate ] -> [  >  Player | > Crate ] (push crate)

([ > Player | Spirit Bush ] -> [ Player | > Spirit Bush ] sfx0 (kick spirit out of bush))
[ Player no Bush | Spirit Bush ] -> [ Player | > Spirit Bush TempVacatedBush] again (kick spirit out of bush)
late [ Player no Bush | Spirit Bush | no Crate no Wall ] -> [ Player no Bush | Spirit Bush | no Crate no Wall ]  sfx0 (play sound)

[ > Player Bush | Spirit ] -> [ > Player Bush | > Spirit] (kick spirit from bush - this is only useful with one spirit if the spirit is also in a bush)

[ > Player Bush ] -> [ > Player Bush TempVacatedBush] (kill bush that you walked through)

late [TempVacatedBush Bush no Player no Spirit no Crate ] -> [ DestroyedBush ] sfx1 (destroy bushes if there is nothing occupying them)
late [TempVacatedBush] -> []

(late [Player Bush] -> [Player Bush]  (player bush sound))

late [Crate Water] -> [StoneCrateSink0 Water] (again) (destroy crate in water)
(late [Crate Bush] -> [Crate DestroyedBush] sfx1)


(check lines of sight from spirit)
[Blocking] -> []
up [ Spirit | ... | Opaque ] -> [ Spirit | ... | Opaque BlockingU]
down [ Spirit | ... | Opaque ] -> [ Spirit | ... | Opaque BlockingD]
right [ Spirit | ... | Opaque ] -> [ Spirit | ... | Opaque BlockingR]
left [ Spirit | ... | Opaque ] -> [ Spirit | ... | Opaque BlockingL]

(make spirit move away)
[ Player no Bush | ... | Spirit no Bush | no wall no crate] -> [ Player | ... | > Spirit | no wall no crate]
(cancel spirit movement if line of sight blocked) 
up [up Spirit][ Player | ... | BlockingD ] ->  [ stationary Spirit][ Player | ... | BlockingD ]
down [down Spirit][ Player | ... | BlockingU ] ->  [ stationary Spirit][ Player | ... | BlockingU ] 
right [right Spirit][ Player | ... | BlockingL ] ->  [ stationary Spirit][ Player | ... | BlockingL ] 
left [left Spirit][ Player | ... | BlockingR ] ->  [ stationary Spirit][ Player | ... | BlockingR ] 
 

[stationary SpiritStationary Target] -> win

(late [Player | ... | Spirit | no Wall no Crate ] -> [Player | ... | Spirit | no Wall no Crate ] again)
[moving spirit] -> [moving spirit] again

(----------------- Aesthetics ---------------------)

(create side borders)
up [ Water | no Water ] -> [ Water WaterCoverUp | ]

(crate sinking animation)
[ StoneCrateSink9 ] -> []
[ StoneCrateSink8 ] -> [ StoneCrateSink9 ] 
[ StoneCrateSink7 ] -> [ StoneCrateSink8 ] 
[ StoneCrateSink6 ] -> [ StoneCrateSink7 ] 
[ StoneCrateSink5 ] -> [ StoneCrateSink6 ] 
[ StoneCrateSink4 ] -> [ StoneCrateSink5 ] 
[ StoneCrateSink3 ] -> [ StoneCrateSink4 ] 
[ StoneCrateSink2 ] -> [ StoneCrateSink3 ] 
[ StoneCrateSink1 ] -> [ StoneCrateSink2 ]
[ StoneCrateSink0 ] -> [ StoneCrateSink1 ]  (it looks jank with water covers - also  at the moment player has to wait if in line 										with spirit, due to again being triggered. Some way to avoid this?) 

(delete the spirit if it gets to the edge of the screen)

[ Spirit ] -> [ Spirit SpiritLeftScreen ]
up [ Spirit SpiritLeftScreen | ] -> [ Spirit | ]
[ SpiritLeftScreen Spirit ] -> [ SpiritLeftScreen ]

[ Spirit ] -> [ Spirit SpiritLeftScreen ]
down [ Spirit SpiritLeftScreen | ] -> [ Spirit | ]
[ SpiritLeftScreen Spirit ] -> [ SpiritLeftScreen ]

[ Spirit ] -> [ Spirit SpiritLeftScreen ]
right [ Spirit SpiritLeftScreen | ] -> [ Spirit | ]
[ SpiritLeftScreen Spirit ] -> [ SpiritLeftScreen ]

[ Spirit ] -> [ Spirit SpiritLeftScreen ]
left [ Spirit SpiritLeftScreen | ] -> [ Spirit | ]
[ SpiritLeftScreen Spirit ] -> [ SpiritLeftScreen ]

(collision effects)
[TempSpiritAlreadyCollided no Spirit] -> []
up 		[ Player | ... | Spirit no TempSpiritAlreadyCollided no bush | Wall ] -> [ Player | ... | SpiritHitsU TempSpiritAlreadyCollided | Wall ] sfx3
down 	[ Player | ... | Spirit no TempSpiritAlreadyCollided no bush | Wall ] -> [ Player | ... | SpiritHitsD TempSpiritAlreadyCollided | Wall ] sfx3
right 	[ Player | ... | Spirit no TempSpiritAlreadyCollided no bush | Wall ] -> [ Player | ... | SpiritHitsR TempSpiritAlreadyCollided | Wall ] sfx3
left 	[ Player | ... | Spirit no TempSpiritAlreadyCollided no bush | Wall ] -> [ Player | ... | SpiritHitsL TempSpiritAlreadyCollided | Wall ] sfx3

up 		[ Player | ... | Spirit no TempSpiritAlreadyCollided no bush | Crate ] -> [ Player | ... | SpiritHitsU TempSpiritAlreadyCollided | Crate ] sfx4 sfx5
down 	[ Player | ... | Spirit no TempSpiritAlreadyCollided no bush | Crate ] -> [ Player | ... | SpiritHitsD TempSpiritAlreadyCollided | Crate ] sfx4 sfx5
right 	[ Player | ... | Spirit no TempSpiritAlreadyCollided no bush | Crate ] -> [ Player | ... | SpiritHitsR TempSpiritAlreadyCollided | Crate ] sfx4 sfx5
left 	[ Player | ... | Spirit no TempSpiritAlreadyCollided no bush | Crate ] -> [ Player | ... | SpiritHitsL TempSpiritAlreadyCollided | Crate ] sfx4 sfx5

([ stationary Player ][ SpiritHitsU Timer5 ] -> [ stationary Player ][ SpiritStationary ]
[ stationary Player ][ SpiritHitsU Timer4 ] -> [ stationary Player ][ SpiritHitsU Timer5 ]
[ stationary Player ][ SpiritHitsU Timer3 ] -> [ stationary Player ][ SpiritHitsU Timer4 ]
[ stationary Player ][ SpiritHits Timer2 ] -> [ stationary Player ][ SpiritStationary ])
[ stationary Player ][ SpiritHits Timer1 ] -> [ stationary Player ][ SpiritStationary ]
[ stationary Player ][ SpiritHits Timer0 ] -> [ stationary Player ][ SpiritHits Timer1 ]
[ stationary Player ][ SpiritHits no Timer] -> [ stationary Player ][ SpiritHits Timer0 ]


(movement sprites)
up 		[ > Spirit no bush | no wall no Crate ] -> [ > SpiritMovesU | ]
down 	[ > Spirit no bush | no wall no Crate ] -> [ > SpiritMovesD | ]
right 	[ > Spirit no bush | no wall no Crate ] -> [ > SpiritMovesR | ]
left 	[ > Spirit no bush | no wall no Crate ] -> [ > SpiritMovesL | ]

[ stationary spirit no TempSpiritAlreadyCollided ] -> [ stationary SpiritStationary ]
late [ Spirit Bush ] -> [ SpiritStationary Bush ]

(delay to let running sounds keep going after spirit leaves the screen)
[ stationary Player ][ SpiritLeftScreen Timer5 ] -> [ stationary Player ][]
[ stationary Player ][ SpiritLeftScreen Timer4 ] -> [ stationary Player ][ SpiritLeftScreen Timer5 ] sfx2
[ stationary Player ][ SpiritLeftScreen Timer3 ] -> [ stationary Player ][ SpiritLeftScreen Timer4 ]
[ stationary Player ][ SpiritLeftScreen Timer2 ] -> [ stationary Player ][ SpiritLeftScreen Timer3 ]
[ stationary Player ][ SpiritLeftScreen Timer1 ] -> [ stationary Player ][ SpiritLeftScreen Timer2 ] sfx2
[ stationary Player ][ SpiritLeftScreen Timer0 ] -> [ stationary Player ][ SpiritLeftScreen Timer1 ]
[ stationary Player ][SpiritLeftScreen no Timer] -> [ stationary Player ][ SpiritLeftScreen Timer0 ] sfx2



==============
WINCONDITIONS
==============

=======
LEVELS
=======
(
message Every year, biologists ___ the rare ____ . But they have to catch them first.

messages can be about the strange behaviours/properties of the species.
)

~~~~~~~~~~
~~~#...#.~
~~.#o....~
~........~
~........~
~~#.s...~~
~~......#~
~.....p.#~
~~.###...~
~~~~~~~~~~

~~~~~~~~~~~
~~~~~~~..~~
~~~###...~~
~~~....#.~~
~~...p....~
~~........~
~~..s.o#..~
~##...##.~~
~~.......~~
~~~.....~~~
~~~~~~~~~~~


~~~~~~~~~~~~
~~~~####~#~~
~####..o~~~~
~~........~~
~~.#..s...~~
~~......p.~~
~~~##...###~
~~~~#.#.#~~~
~~#~~...~~~~
~~~~~~~~~~~~


~~~~~~~~~~~~
~~~....##~~~
~~#.#.....~~
~#...~.#...~
~#.#.~~.s..~
~~..~~~..p.~
~~~..~~~.##~
~~~~.~~~o#~~
~~~~#~#~~#~~
~~~~~~#~~~~~
~~~~~~~~~~~~


~~~~~~~~~~~~~~~
~~~#~..~..~~~~~
~.~#.......~~~~
~~~#........~~~
~~.#o.*s..#.~~~
~~~.......#..~~
~.~#.p.......~~
~.~~~......~~~~
~~~~~~#.~~~~#~~
~~~~~~~~~~~~~~~

~~~~~~~~~~~~~
~~~~~..~~~~~~
~~~~.....###~
~~###.....o#~
~~#........#~
~.#..s......~
~...........~
~..*....##.~~
~~....~.##.~~
~~.p.~~....~~
~~~~~~~~~~~~~

~~~~~~~~~~#~~~
~#~~~~~~~~~#~~
~~~~~....~~~~~
~~~...*..~~#~~
~~....o...~~#~
~~..p.*...~~#~
~~.......~~~#~
#~....s..~~#~~
~~~......~~#~~
~#~~~~..~~~~#~
~#~~~~~~~~#~~~

.....~~~~~~~~
#########~~~~
#.......#.~~~
#.*~~~.o#...~
#..~~~......~
#..~~~..s...~
#..~~~......~
#.*~~~.p#...~
#.......#..~~
#########.~~~

~~~~~~~~~~~~~~~
~~...~~~~~..#~~
~~.s.~~~~~..#~~
~~...~~~~~..#~~
~~...~~~~~..#~~
~~...#...#..#~~
~~.o.*.p.*..#~~
~~..........#~~
~~~~~~~~~~~~~~~ (send back the way it came with crate)

.b..b~~~~~~~~
##.######~~~~
#.......#.~~~
#.*~~~.o....~
#..~~~....#.~
#..~~~..s...~
#..~~~.....#~
#.*~~~.p#...~
#.......#b.~~
#########.~~~


~~~~~~~~~~~~~
~~~~~....~~~~
~~.......~~~~
~#*##*#....~~
~..*..#~...#~
~p.#..#~~..#~
~..#.~~~w*.#~
~~.#~~~#~~..~
~~~#~~~#*...~
~~~~~#~.o.#~~
~~~~~~~~#~~~~
~~~~~~~~~~~~~  (level introducing destroying crates)


####~~~######
##~~~~~~~...#
#...**.*....#
#po*.**...s.#
#..*..*.*...#
##~~~~~~~...#
####~~~######  (destroy crates that are in the way)




(put some bushes you must break in earlier levels, to show that you can)





(levels introducing bushes as limited use cover)

~~~~~~~~~~~~~~~~
~~~~#~~~~~~~~~~~
~~#~~~.b..~~~~~~
~~~~#...b.b~#~~~
~~~~##b##....~~~
~~~......~.#b.~~
~~#.#os.b~~~..~~
~~#.....b~~.#.~~
~~##.....~~..b~~
~~~~..##~~~p.~~~
~~~~~#~~~~~~~~~~
~~~~~~~~~~~~~~~~

(
~~~~~~~~~~~~~~~~
~~~~#~~~~~~~~~~~
~~#~~~.b..~~~~~~
~~~~#...b.b~#~~~
~~~~##b##....~~~
~~~......~.#b.~~
~~#.#.s.b~~~..~~
~~#.....b~#...~~
~~##.....~~..b~~
~~~~..##~~~p.~~~
~~~~~#~~~o~~~~~~
~~~~~~~~~#~~~~~~
~~~~~~~~~~~~~~~~ (simplify to put focus on the important discovery?))

~~~~~~~~~~~~~~~
~~~~#~~#~~~~~~~
~~~...~#~~~~~~~
~~..#~~~~~~b~~~
~~b.#~#~~##.#~~
~~....#~..b..~~
~b.#..........~
~..b....s.....~
~..........#..~
~..p.bo###...~~
~~b..####b.~~~~
~~~...~~~~~~~~~
~~~~~~~~~~~~~~~


(levels introducing bushes as catching spirit)


~~~~~~~~~~~~~~
~~~~~...~~~~~~
~~....b.#~##~~
~~.b.........~
~............~
~#b...s...#..~
~#.bo#.......~
~#..b~~b...b~~
~~.p.#~~~...~~
~~~~##~~#~~~~~
~~~~~~~~~~~~~~ (makes it look like bushes will stop spirit like walls) (maybe add an animation for entering bush for the first time?)
(level ends too soon (graphics bug))


~~~~~~~~~~~
~#~...~...~
~~~.~.~.~.~
~..b#b.h..~
~.~.~.~.~.~
~p.b.b.b..~
~.~.~.~.~.~
~..j.b#b..~
~.~.~.~.~~~
~...~...~#~
~~~~~~~~~~~

~~~~~~~~~~~~~~~~~
~~~~~~~~~......~~
~~....~....~~...~
~...b.~~.b..~b..~
~~~....~~~~.~~~~~
~~........~.~..~~
~~~.b..b.w~..b..~
~~~~~~....~~...~~
~~~..~.........~~
~...b..o.b.p.b~~~
~~.....#....~~~~~
~~~~~~~~~~~~~~~~~ (can't walk adjacent to bush w/ spirit (must be on correct side of it). Use Bush as Cover.)

(
~~~~~~~~~
~~.....~~
~.b.b#h.~
~..b.j..~
~.#.b.b.~
~..b.b..~
~.b.b.b.~
~~...p.~~
~~~~~~~~~
)

(level where you must leave at least one bush per col as cover?)

(
~~~~~~~~~
~.#.....~
~...~bo#~
~.s.~...~
~#..~...~
~..p~#..~
~...~..#~
~.#.~...~
~~~~~~~~~ (new path opens because bush is used?)
)

(intentionally break bushes)

~~~~~~~~~~~~
~~~.#.~~~~~~
~~......o#~~
~~.#b~.....~
~#.~~~~....~
~...~~~~...~
~~..s.~~b.#~
~~p.......~~
~~~.b...#~~~
~~~~~~~~~~~~ (bush you can't break and run until moving the spirit)(forces you to do a circuit)


~~~~~~~~~~~~~~~~~~~
#o~b~.b.~~~~~~~~~~~
~.....~.~~~~~~~~~~~
#.#~~~~b~.b.h.~b~b.
#.#~......~...~.~..
#..b...##.~.p...~..
#####..##.#........  (intentionally breaking bushes, and progress gated by bushes that must be used constructively)


~~~~~~~~~~~~~
~####~#~...#~
~#o..~b~w~b#~
~#.~...~~~.#~
~~.~~~~~b...~
~~.~~~~~~~~.~
~#.#b.b..b~.~
~#.~~.~.#~~.~
~#...p......~
~~~~~~~~~~~~~ (intentionally break so it won't block line of sight)(break when in view of spirit / break and go back)


(
~~~~~~~~~~~~~~~
~~~~#~~~~~~~~~~
~~#~##~~~~~.~~~
~~#~..~~~b~o#~~
~~~~.bb.~b...~~
~#~~...~~..p..~
~~~~~..~......~
~.#~.#b..s.#.~~
~..###...b...~~
~~.##~~..#...~~
~~#.#~~#~~#.~~~
~~~~~~#~~~~~~~~
~~~~~~~~~~~~~~~ (breaking bushes)(focus on double bushes) (two bushes are red herrings. Is this bad?)
)




(hiding in bush adjacent to spirit in bush, and pushing spirit free)


~~~~~~~~~~~~~~~~~~~
~~~~~~~..#.~~~###~~
~~...b...#..b..o#~~
~xs..b.~b...b.~.~~~
~##..b....#.b.....~
~#..~~~..~#.....#.~
~~#~~~~~#~#~~##...~
~~~~~~~~~~~~~~~~~~~ 


(
~~~~~~~~~~~~~~~~~~~~
~#~~~~~~~~~~~~#~#~~~
~~~..b......b.#~~~~~
~xs..b.~b...b.#o...~
~##..b....#.b....b.~
~~~~~~~~~~#......b.~
~~~~~~~~#~#~~~~~~~~~
~~~~~~~~~~~~~~~~~~~~

~~~~~~~~~~~~~~~~
~~~~~~~#..~~~~~~
~~...b......b.~~
~xs..b.~b...b.~~
~##..b....#...#~
~#..~~~..~#..o#~
~~#~~~~~#~#~~##~
~~~~~~~~~~~~~~~~

~~~~~~~~~~~~~~~~~~
~~~~~~~..#.~~~###~
~~...b...#..b..o#~
~xs..b.~b...b.~.~~
~##..b....#.b...~~
~#..~~~..~#.....#~
~~#~~~~~#~#~~##~~~
~~~~~~~~~~~~~~~~~~ 

)


(double bushes and pushing spirit free)

~~~~~~~~~~~~~~~
~~#~~~..~~~~#~~
~~~~.bb.~..~#~~
~~.......b.~~~~
~#.b.b...b..b.~
~..b.b.p....h.~
~.............~
~~##..obb..#~~~
~~~~~~####~~~~~
~~~~~~~~~~~~~~~


(line of bushes)

~~~~~~~~~~~~~
~~~.#.....~~~
~.....p.#...~
~.hbbbbbbbo.~
~..#.......#~
~.....#.....~
~~~~#....~~~~
~~#~~~~#~~~~~
~~~~~~~~~~~~~








(pushing spirit in field of bushes)
(
~~~~~~~~~
~.bbbbb.~
~p~hjbb.~
~~bb~b~~~
~.bbbbb.~
~...~...~
~~~~~~~~~)


(intentionally breaking + hiding in bush next to spirit)
~~~~~~~~~~~
~~~.b.~...~
~.bb~b..j.~
~.~.b.~...~
~...~~~~~~~
~~~b....b.~
~..b.b~bs.~
~b~b.~~.~.~
~.~~~~~.#.~
~...p.....~
~~~~~~~~~~~




(crates and bushes)
(
~~~~~~~~~~~~~
~~~...###~~~~
~~.p.......#~
~~..b~*~b..~~
~#.#..s~..##~
~~~#o.....~~~
~~#~~..#~##~~
~~~~~~~~~~~~~ (too similar?)
)
~~~~~~~~~~~~
~##.p.~~~~~~
~.o.......~~
~.*.b~~b..~~
~........##~
~~.....s.~~~
~~....~~##~~
~~~~~~~~~~~~



~~~~~~~~~~~~~
~~.....~~~#~~
~.h.*.b..~~~~
~.........~#~
~.....b.b.~~~
~.....~~..~~~
~~~.p.~~bo#~~
~#~...~~~~~#~
~~~~~~~~~~~~~




(place crate next to spirit in bush so you can walk past without breaking it)

~~~~~~~~~~~~~~~~~
~...............~
~.b.p.h.b.*..bb.~
~.......#.#.o...~
~~#~~~#~#~#~#~~~~
~~~~~~~~~~~~~~~~~



(Push crate in bush to get it past spirit)

~~~~~~~~~~~~~~
~~~~~~~####~~~
~~...#~o..~~~~
~~........~~~~
~##b..s..b##~~
~~...##~#..~~~
~~.........~~~
~~..*.p.b..##~
~#####~~#..~~~
~~~~~~~~~~~~~~

~~~~~~~~~~
~.....###~
~........~
~.s.b~b~b~
~#~......~
~#o.p.*..~
~#~......~
~~~~~~~~~~ (ok) (combine with other level?) 

(collide spirit with crate in bush)





(
~~~~~~~~~~~~~
~....~~~~~~~~
~....b......~
~...........~
~b...s....b~~
~....~......~
~.*..b...*..~
~....~.p....~
~~~~~~~~~~~~~ (create permanent pathway with crate?)
)



(level where you must get both crates to the same place?)


(level where you must push a crate through bushes and conserve them?)



(push crate in bush so that spirit can hit it)

~~~~~~~~~~~~
~~~#.......~
~#~#.p...%.~
~~~#.....w.~
~~~~~~.~~~.~
~#o.b~b~...~
~#.~.~.#.##~
~#.~.....##~
~~~~~~~~~~~~ 

(crate and double bushes)
~~~~~~~~~~~~~~~
~##~..........~
~.....bb...~~.~
~..p.....~.bb.~
~.....~~......~
~.~.*.......s.~
~.o........~..~
~~~~~~~~~~#~~~~
~#~~##~~#~~~#~~
~~~~~~~~~~~~~~~

(Push crate in bush to get it past spirit in bush)

~~~~~~~~~~~~~~~~~~
~.....#...~#.~.~#~
~..b......~#o.b~.~
~.%hb~~~~.~#..b#.~
~..b.~#...~.b....~
~....~#b.b..b%.##~
~#.p.~#~#~~....##~
~~~~~~~~~~~~~~~~~~ 



(
~~~~~~~~~
~....%~~~
~.......~
~..p...%~
~.......~
~.%...%.~
~~~~~~~~~(lvl with crates already inside?)
)




message The End. Thanks for playing!








(misclevels)


~~~~~~~~~~~~~~~
~~~...~~~....~~
~~..b...~b.b..~
~~.b..b......b~
~#..#o....s..#~
~#.........#~~~
~....p.b.b..~~~
~~b#.........~~
~~~#..b...b.#~~
~~~##.......~~~
~~~~~~~#...~~~~
~~~~~~~~~~~~~~~ 


~~~~~~~~~
~~...*.#~
~~*..*.#~
~#*~.#~~~
~..*so.*~
~p#.....~
~.*.~**.~
~~##~~##~
~~~~~~~~~

~~~~~~~~~~~
~~~~#~~~~~~
~~...~...~~
~~.......~~
~~..#..~~~~
~...#..s~~~
~....o....~
~##*#*#.#.~
~~..#.....~
~~....p~~~~
~~~.#~~~~~~
~~~~~~~~~~~ (meh?) (fill gaps)

~~~~~~~~~~~~~~~~~~~
~~.#....~~~~~~~~~~~
~.....p.#~~#....~~~
~..~~~...~~.....#~~
~...~~~..~~..~#...~
~.b.~~~.~~...~~.o#~
~....~..~~...~~...~
~.s.#~...~..~#~...~
~~..~~......#~~#..~
~~...~~#....~~...~~
~~~~~~~~~~~~~~~~~~~ 

~~~~~~~~~~~~
~~~~~~...~~~
~~~......~.~
~#~*##*#.~~~
~~..*..#..~~
~#p.#..#..~~
~#..#~~.s..~
~~#~~......~
~#o.~~....~~
~####~~#...~
~~~~~~~~~~~~

~~~~~~~~~~~~~
~~~##~~~~###~
~###o.b.p..#~
~#.....~...#~
~#bb..b.bhb#~
~#bb...~~~##~
~##~~~~~~~~~~
~~~~~~~~~~~~~

#########
~o~~~~~p~
~.bbbbb.~
~~.~b~.~~
~~.~h~.~~
~.bbbbb.~
~.~~~~~.~
#########


~~~~~~~~~~~~~~
~~~~~~...~~~~~
~~.......~#~~~
~~..~~.b.~~~#~
~#.........o#~
~#..bb.s.b##~~
~#~#..~....~~~
~~~~~.##...~#~
~~~#~....*.~~~
~~~~~.p....~~~
~~~~~~~~~~~~~~


~~~~~~~~~~~~~~~~~
~~~~~...~~~~~~~~~
~~~~~....~~~..~~~
~~~~....s~....~~~
~~~~.*...~~~~..~~
~~~~.#.*....~~..~
~#o.........~~.~~
~~~~~.~~....~~.~~
~~~~~p....~~~..~~
~~~~~~~~.~~...~~~
~~~~~~~~..~..~~~~
~~~~~~~~....~~~~~
~~~~~~~~~~~~~~~~~





(misc non-levels)


~~~~~~~~~~~
~~~~~~####~
~~~~~~#...~
~####~#*..~
~...#~#...~
~....s....~
~.........~
~..*.#....~
~.........~
~.p.......~
~~~~~~~~~~~
~~~~~~~~~~~ (level where you recover box)





~~~~~~~~~~~~~
~~~~~~###~~~~
~~~~~#.o.~~~~
~~~.......~~~
~~~.#...s.~~~
~~........#~~
~~.#.......~~
~~......#..~~
~~#.p......~~
~~~......#.~~
~~~~~~~~~~~~~
~~~~~~~~~~~~~

~~~~~~~~~~~~~~~
~~~~~.#....~~~~
~~~.....#.....~
~~........#...~
~..b........#.~
~.....s....b..~
~.b.p......#..~
~......b.#.~~~~
~~..b..#...~~~~
~~~...~~~~~~~~~
~~~~~~~~~~~~~~~


~~~~~~~~~~~~~~~
~b.b.b~#~b.b.b~
~.~.~.~o~.~.~.~
~b.b.h.b.b.b.b~
~.~.~.~~~.~.~.~
~b.b.b.b.b.b.p~
~.~.~.~~~.~.~.~
~b.b.b~~~b.b.b~
~~~~~~~~~~~~~~~


...............
.~.b.~.b.~.b.~.
b.b~b.b~b.b~b.b
~.~~~.~~~.~~~.~
~b.b.b~w~b.b.b~
~~~.~~~~~~~.~~~
~~~p...b....~~~
~~~~~~~~~~~~~~~

~~~~~~~~~~~~~
~...........~
~...b....b..~
~.b....b....~
~....b....b.~
~..b....b...~
~.....b.....~
~.p.b....b..~
~...........~
~~~~~~~~~~~~~









~~~~~~~~~~~~~
~~~.....~~~~~
~~........~~~
~..........~~
~..g.s..o#.~~
~..........~~
~...#......~~
~~.........~~
~~~....p..~~~
~~~~~~~~~~~~~

~~~~~~~~~~~~~~
~~~~~~~~~..~~~
~~~~........~~
~~......g...~~
~~..........~~
~~~...s...o.~~
~~~.........~~
~~~~....*...~~
~~~~~.......~~
~~~~~..p...~~~
~~~~~~~~~~~~~~

~~~~~~~~~~~~~~~
~~..........~~~
~.......##...~~
~##.....##...~~
~##.to....t..~~
~...t..s..t..~~
~...t.....t..~~
~...##.......~~
~...##...p..~~~
~~.........~~~~
~~~~~~~~~~~~~~~


~~~~~~~~~~~~~~
~~~..........~
~~...........~
~~.......g.p.~
~..#.........~
~.........*o.~
~......s.....~
~...#........~
~~...........~
~~~.........~~
~~~~~~~~~~~~~~