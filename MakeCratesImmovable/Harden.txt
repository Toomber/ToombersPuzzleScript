title Harden
(opportunity cost
 Funnel
 Cornered
 Reduction
 Depletion
 Static
 Stabilise
 Optionless 
 Pin
 Stillness Reigns
 Principle of least actions
 Loose Cargo
 
 Exhaustion
 Secure
 Harden
)
author Toombler
homepage https://toombler.itch.io/

background_color #132a13 (#1f2041)

run_rules_on_level_start

(verbose_logging)

========
OBJECTS
========

(palettes:
blues
#1f2041
#4b3f72
#ffc857
#119da4
#19647e

greens
#132a13
#31572c
#4f772d
#90a955
#ecf39e
#ffc857
)

Background
#31572c

(LIGHTGREEN GREEN
11111
01111
11101
11111
10111)


Platform
#31572c #3f7338 #3b6935 #396334 #284a23   
00000
01010
00000
01010
00000



(10220
02212
21001
00212
21122)
(#119da4)

Player
grey grey grey grey grey (Black Orange White Blue)
.000.
.111.
22222
.333.
.3.3.

Crate
#ffc857 (Orange Yellow)
00000
0...0
0...0
0...0
00000

CrateCannotBePushed
orange
00000
0...0
0...0
0...0
00000

Wall
#90a955 (
#8ea35b #95b056 #90a955 
10220
02212
21001
00212
21122 )(BROWN DARKBROWN
00010
11111
01000
11111
00010)

WallCoversDone
transparent

WallCoverUp
#9bba50 #2c757d
00000
.....
.....
.....
.....

WallCoverDown
#9bba50 #2c757d
.....
.....
.....
.....
00000

WallCoverRight
#9bba50 #2c757d
....0
....0
....0
....0
....0

WallCoverLeft
#9bba50 #2c757d
0....
0....
0....
0....
0....

WallCoverUpRightCorner
#9bba50 #2c757d
....0
.....
.....
.....
.....

WallCoverDownRightCorner
#9bba50 #2c757d
.....
.....
.....
.....
....0

WallCoverUpLeftCorner
#9bba50 #2c757d
0....
.....
.....
.....
.....

WallCoverDownLeftCorner
#9bba50 #2c757d
.....
.....
.....
.....
0....


FillMarker
black

WinMarker
transparent


=======
LEGEND
=======

. = Background
% = Platform
# = Wall
P = Player
Q = Player and Platform
* = Crate

BlocksPlayer = Crate or Wall
BlocksCrate = Crate or Wall or Platform

WallCover = WallCoverUp or WallCoverDown or WallCoverRight or WallCoverLeft or WallCoverUpRightCorner or WallCoverDownRightCorner or WallCoverUpLeftCorner or WallCoverDownLeftCorner

=======
SOUNDS
=======

Crate MOVE 36772507
Player MOVE 54585507

endlevel 81161508

================
COLLISIONLAYERS
================

Background
Platform
Player, Wall, Crate
CrateCannotBePushed
WallCoversDone,
WallCoverUp,
WallCoverDown,
WallCoverRight,
WallCoverLeft,   
WallCoverUpRightCorner,   
WallCoverDownRightCorner,   
WallCoverUpLeftCorner,   
WallCoverDownLeftCorner,
FillMarker
WinMarker

======
RULES
======


[ >  Player | Crate ] -> [  >  Player | > Crate  ]
[ > Crate | Platform ] -> [ Crate | Platform ]  


late [ Player ] -> [ Player FillMarker ]
late [ FillMarker | no BlocksPlayer ] -> [ FillMarker | FillMarker ]

late [ CrateCannotBePushed ] -> []
late [ Crate ] -> [ Crate CrateCannotBePushed ]
late [ FillMarker | Crate CrateCannotBePushed | no BlocksCrate ] -> [ FillMarker | Crate | no BlocksCrate ]
late [ FillMarker ] -> []

late [ Player ] -> [ Player WinMarker ]
late [ WinMarker ][ Crate no CrateCannotBePushed ] -> [][ Crate ]
late [ WinMarker ] -> win


(----------graphics----------)



(create side borders)
late up [ Wall no WallCoversDone | no Wall ] -> [ Wall WallCoverUp | ]
late down [ Wall no WallCoversDone | no Wall ] -> [ Wall WallCoverDown | ]
late right [ Wall no WallCoversDone | no Wall ] -> [ Wall WallCoverRight | ]
late left [ Wall no WallCoversDone | no Wall ] -> [ Wall WallCoverLeft | ]


(create corners)
late down [ Wall no WallCoversDone | ] -> [ Wall WallCoverDownRightCorner WallCoverDownLeftCorner | ] 
late up [ Wall no WallCoversDone | ] -> [ Wall WallCoverUpRightCorner WallCoverUpLeftCorner | ]

(remove extra corners)
late up [ Wall WallCoverUpRightCorner no WallCoversDone | no WallCoverRight ] -> [ Wall | ] 
late right [ Wall WallCoverUpRightCorner no WallCoversDone | no WallCoverUp ] -> [ Wall | ] 
late down [ Wall WallCoverDownRightCorner no WallCoversDone | no WallCoverRight ] -> [ Wall | ] 
late right [ Wall WallCoverDownRightCorner no WallCoversDone | no WallCoverDown ] -> [ Wall | ] 
late up [ Wall WallCoverUpLeftCorner no WallCoversDone | no WallCoverLeft ] -> [ Wall | ]
late left [ Wall WallCoverUpLeftCorner no WallCoversDone | no WallCoverUp ] -> [ Wall | ] 
late down [ Wall WallCoverDownLeftCorner no WallCoversDone | no WallCoverLeft ] -> [ Wall | ] 
late left [ Wall WallCoverDownLeftCorner no WallCoversDone | no WallCoverDown ] -> [ Wall | ] 

(finish covering walls)
late [ Wall no WallCoversDone ] -> [ Wall WallCoversDone] 

(--------------------)

(animation where crates solidify at end of level)
(visual feedback for crates which can't move - is this a good idea in terms of surprise?)
(plant themed?)
(idea: holes which can destroy crates but which fill and open up new possibilities for crates to be moved?
       More things you can interract with and make inert:
       Powerup which gives you a temporary trail you can block yourself with?
       Balloon thing that is pushed off wall if you walk into it, but is destroyed by crate? 
)        
(flame -> region of effect highlighted)

==============
WINCONDITIONS
==============


=======
LEVELS
=======

#######
#*#.#.#
#.#.#*#
#.#*#.#
#.....#
#..p..#
#######

##########
#....*.###
#...######
#....*.p.#
#...######
#.*...####
##########


#######
#.....#
#.....#
#.p.*.#
#.....#
#.....#
#######


###%#%##
###%.%%%
#%%%...#
#%*....#
#%%%%%%#
###...%#
###.p.%#


%##%%%#%%
%#.....%%
%%...*.#%
#%.....%%
%%.*.p.%#
%%.....%%
%%%##%%%%

%%%%%%%
%.....%
%.*.*.%
%..p..%
%.*.*.%
%.....%
%%%%%%%









%%%%###
%.*.%%#
%*..%q#
%.*.%%#
%%%%###

%%#%%#####
%....#%%%%
#.*...%*.%
#.*.p.%*.%
%....#%%%%
%%#%%#####

%%#%%#####
%....#%%%%
#.*.p.%*.%
%....#%%%%
%%#%%#####

(%%#%%#%#%%
%....#%..%
%.*%.....%
%..%#%.p.% 
%##%#%%%#%)




%%%%%#%%
%.*.%..%
%...#..%
##%.*.p%
%...#..%
%.*.%..%
%%%%%#%% (bit broken - can make 2by2 with crates and wall)










%%%#%%%%%%
%..#..%.*%
%p.**.%..%
%..#..%%%%
%%%#%%%%%%

%%%%#%%
#.p.*%%
%*..*%%
%%###%%
%%%%%%%


