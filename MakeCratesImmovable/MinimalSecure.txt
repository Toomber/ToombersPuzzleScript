
title Secure	

author Toombler
homepage https://toombler.itch.io/

background_color #132a13 (#1f2041)
(maybe add some brown?)

run_rules_on_level_start

again_interval 0.4

(verbose_logging)


(logo?
#######
#....p#
#.#####
#.....#
#####.#
#*....#
#######
)


========
OBJECTS
========


Background
#31572c


Platform
#31572c #3f7338 #3b6935 #396334 #284a23   
00000
01010
00000
01010
00000


Crate
#ffc857 (Orange Yellow)
00000
0...0
0...0
0...0
00000

CrateCannotBePushed
#d5e352 #cde352 #c8e352 orange
00000
0...0
0...0
0...0
00000

Wall
#132a13 #90a955 

FillMarker
black

WinMarker
transparent

WonCrate
#d5e352 #132a13 (#c1cf4a #cbd94e #d5e352 #b1cc4e #d5e352 #9bba50 #90a955)
00000
01110
01110
01110
00000

Person0
#31572C #416931 #507A36 #608C3A #709D3F #7FAF44 #8FC049 #9ED24D #AEE352  
.544. 
.213. 
31255 
.356. 
.5.5. 
 


=======
LEGEND
=======

. = Background
% = Platform
# = Wall
P = person0
Q = person0 and Platform
* = Crate

BlocksPlayer = Crate or WonCrate or Wall
BlocksCrate = Crate or WonCrate or Wall or Platform  

Person = Person0

Player = Person0

=======
SOUNDS
=======

Crate MOVE  5983507 (21792307) (36772507)
Player MOVE 98574507 (54585507)

(WonCrate create 66651704 )
endlevel 34417108 (39825900) (30978708) (18575908) (17763700) (57685702) (37882503) (81161508)

WonCrate create 91410908 



================
COLLISIONLAYERS
================

Background
Platform, 
Person, Wall, Crate, WonCrate
CrateCannotBePushed
FillMarker
WinMarker

======
RULES
======

(
[ Player ][ WinMarker ] -> [ stationary player ][ WinMarker ]
)

[ >  Player | Crate ] -> [  >  Player | > Crate  ]
[ > Crate | Platform ] -> [ Crate | Platform ]  

late [ Player ] -> [ Player FillMarker ]
late [ FillMarker | no BlocksPlayer ] -> [ FillMarker | FillMarker ]

late [ CrateCannotBePushed ] -> []
late [ Crate ] -> [ Crate CrateCannotBePushed ]
late [ FillMarker | Crate CrateCannotBePushed | no BlocksCrate ] -> [ FillMarker | Crate | no BlocksCrate ]
late [ FillMarker ] -> []

late [ Player ] -> [ Player WinMarker ]
late [ WinMarker ][ Crate no CrateCannotBePushed ] -> [][ Crate ]

late [ WinMarker ][ Crate ] -> [ WinMarker ][ WonCrate ]

[ action Player ][ WinMarker ] -> win

==============
WINCONDITIONS
==============


=======
LEVELS
=======

message Press X to proceed to the next level after solving

(introduction levels)

#######
#*#.#.#
#.#.#*#
#.#*#.#
#.#.#.#
#..p..#
####### (GOOD)(hints at rule being "all in a line")

##########
#....*..##
#...######
#....*.p.#
#...######
#.*...####
########## (GOOD)(maybe just one of these)



#######
#.....#
#.....#
#.p.*.#
#.....#
#.....#
####### (GOOD)

##########
###p*..###
###**....#
#........#
#.......*#
#........#
######...#
########## (level about freely dumping crates in corners)



(levels involving platforms)

##########
###.....%#
###...*.%#
###%####%#
#.%......#
#.#......#
#.#..*%..#
#%%%######
#.p.######
########## (introduction to platforms, no fail states) (GOOD) 

###########
###.....###
###.###.###
###*###%###
#%...%.*.%#
#%.%.....%#
#%...p...%#
########### (crate wins in corner but need corner open to traverse) (possible to chain this?) (GOOD)

##############
#%%%##########
#......#######
#...##.#######
###*##.....###
###.%*..##.###
###.###.##*###
###p###..%..%#
#########%..%#
##############

(
##############
#%%%##########
#......#######
#...##.#######
#.#*##.....###
#.#.##..##.###
#.#.%*..##.###
###.###.##*###
###p###..%..%#
#########%..%#
############## (GOOD))

(not totally happy with this)
(level like this, where: put crate in corner of passage way, and where you came from? (although already a level sort of like that))
(
#############
#%%##########
#.*...#######
#..##.#######
#%%##.....###
##p%*..##.###
######.##*###
######..%..%#
########%..%#
#############)










(
#########
##....*p#
##.##.###
#%.##..%#
#.....*%#
#..######
#%%######
#########

#########
#####%%##
#%...p.##
#%.###.##
##.#.#*##
##.###.%#
##.....%#
##%%#####
#########

#############
####....#####
####.##.*.p##
####.####.###
####.#%%#.###
#%......#.###
#%.#.##...###
##.#.##*#####
##.####.%####
##......%####
##%%#########
#############
)




(
###########
#.........#
#.##%%%#%.#
#.#.....%.#
#.%.*...#.#
#.%...*.%.#
#.#.p...%.#
#.%.....%.#
#.%%%###%.#
#.........#
########### (GOOD) (but maybe not good enough to introduce trick))

(
###########
#####%%#.##
#####..#.##
#####..#.##
#.*.*.*#p##
#.....%#.##
##.#####*##
#%.......%#
#%.......%#
###########

#############
######..#...#
#.*.*...#.p.#
#.......#...#
##.#######*##
#%.........%#
#%.........%#
#############)





(two crates on wall = stuck)
###########
#.........#
#.##%%%#%.#
#.#.....%.#
#.%.*...#.#
#.%...*.%.#
#.#.p...%.#
#.%.....%.#
#.%%%###%.#
#.........#
########### (redundant or useful?)

###########
###%.....%#
#%.....%..#
#..###*####
#*.#...####
#..*.p%####
#..########
########### (GOOD)







#########
#%%..%%%#
#%%%..p%#
#%*..%.%#
#%%%.%#%#
#%#....%#
#%..%%#%#
######### (GOOD)

#########
#%%%%%.%#
#.*...*%#
#%.....%#
#%..*..%#
#%.....%#
#%*.p.*.#
#%.%%%%%#
######### (GOOD)

#########
#*.*.*.*#
#.*.*.*.#
#*.*p*.*#
#.*.*.*.#
#*.*.*.*#
#.*.*.*.#
#*.*.*.*#
######### (kinda fun) (GOOD)

(#.#.#.#.#.#
.#.#.#.#.#.
#.*.*.*.*.#
.#.*.*.*.#.
#.*.*p*.*.#
.#.*.*.*.#.
#.*.*.*.*.#
.#.*.*.*.#.
#.*.*.*.*.#
.#.#.#.#.#.
#.#.#.#.#.#)


(
#########
#%%%%%%%#
#%.....%#
#%*...*%#
##%#%#%##
##%#%#%##
#%*.p.*%#
#%%%%%%%#
#########
)


(levels about getting blocks stuck inside platform maze)
(
##################
#%%%%%%%%%%%%%%%%#
#%*%q%*%.%%%*.%%%#
#%.%%%.%.%%%%.%%%#
#%.%%**..%%*....%#
#%.%%..%.%%%.%%*%#
#%*%%**%%%%%.*%%%#
#%%%%%%%%%%%%%%%%#
##################) (some way to make this a deductive puzzle?)

#########
#%%%%%%%#
#%*%...%#
#%.%.%.%#
#%p%.*.%#
#%.%.*.%#
#%*%.*.%#
#%%%%%%%#
#########

###########
#%%%%%%%%%#
#%%..*..%%#
#%..%%%..%#
#%.......%#
#%**...**%#
#%%%.p.%%%#
#%...%...%#
#%%%%%%%%%#
###########

#########
#%%%%%%%#
#%%*.%q%#
#%%%.%%%#
#%*....%#
#%%.%%*%#
#%%.*%%%#
#%%%%%%%#
#########



(levels about crates being stuck due to inaccessability)

#############
#..%%%%%%%..#
#..%.....##.#
####.*.*.##.#
####.....%..#
#..%.p.*.%..#
#..%.....%..#
#..%%%##%%..#
#############

###########
#.*%#######
###%.*.%###
#.p%.*.%*.#
###%.*.%###
#.*%#######
########### (iconic 3 block puzzle twist) (GOOD)


(
#############
#..%%##%%%..#
#..%.....##.#
#.##.*.*.##.#
#.##.....%..#
#..%.p.*.%..#
#..%.....%..#
#..##%%%%##.#
############# (not sure on order of this) (GOOD)
)

(
#############
###%%%%%#%%##
#%%%.*.%..%##
#%#....#...%#
#%#*%%.*.p.%#
#%#....#...%#
#%%%.*.%..%##
###%%%%%#%%##
#############)

#############
#%.........%#
#%.%%%%%%%.%#
#%.%**.**%.%#
#%.%*...*%.%#
#%p%..*..%*%#
#%.%*...*%.%#
#%.%**.**%.%#
#%.%%%%%%%.%#
#%.........%#
#############




