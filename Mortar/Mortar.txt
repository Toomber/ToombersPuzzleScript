title Mortar
author Toomber
homepage www.puzzlescript.net

(again_interval 0.05)

realtime_interval 0.08

background_color #3c1518

(
========
COLOURS
#001427 blue
#708d81 grey
#f4d58d yellow
#bf0603 red
#ba8712 #5d4409 browns
)

========
OBJECTS
========

Background
#d58936 #d58936  (#ba8712) (#fbc40e)
11111
01111
11101
11111
10111


Target
#bf0603
.....
.000.
.0.0.
.000.
.....

Wall
 #D65636 #69140e (#c33f28) (#708d81 #001427)
00010
11111
01000
11111
00010

Person
#bf0603 (#8e1c00) (#69140e) 
.000.
.000.
00000
.000.
.0.0.
(
.000.
.111.
22222
.333.
.3.3.
)

Crate
#b14700 (#a44200) (#001427)
00000
0...0
0...0
0...0
00000

PersonInside
#D65636 (#708d81) transparent #bf0603
00010
11111
01000
11111
00010

SolidWall
#3c1518 (#4f635b) (#708d81) (#000000 #1F1F1F #3F3F3F #5F5F5F #7F7F7F #9F9F9F #BFBFBF #DFDFDF #FFFFFF)   

TempPersonExistsAlready
white

TempCannotPushCrate
white

pattern0
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
65445 
53236 
41146 
32355 
44554 
 
pattern1
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
65445 
64235 
42146 
32355 
44554 
 
pattern2
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
66544 
65335 
52135 
42245 
44454 
 
pattern3
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
56544 
76335 
63125 
42245 
43444 
 
pattern4
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
56544 
66434 
64225 
42135 
43344 
 
pattern5
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
45543 
67534 
65224 
53135 
33344 
 
pattern6
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
45543 
67533 
66324 
54224 
43334 
 
pattern7
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
34543 
57633 
67423 
54224 
43334 
 
pattern8
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
34543 
46643 
67533 
55324 
43334 
 
pattern9
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
23443 
36642 
57633 
56423 
43334 
 
pattern10
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
23443 
35652 
47742 
56533 
44334 
 
pattern11
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
23443 
24653 
46742 
56533 
44334 
 
pattern12
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
22344 
23553 
36753 
46643 
44434 
 
pattern13
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
32344 
12553 
25763 
46643 
45444 
 
pattern14
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
32344 
22454 
24663 
46753 
45544 
 
pattern15
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
43345 
21354 
23664 
35753 
55544 
 
pattern16
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
43345 
21355 
22564 
34664 
45554 
 
pattern17
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
54345 
31255 
21465 
34664 
45554 
 
pattern18
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
54345 
42245 
21355 
33564 
45554 
 
pattern19
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
65445 
52246 
31255 
32465 
45554 





=======
LEGEND
=======

. = Background
# = Wall
% = SolidWall
P = Person
Q = PersonInside
* = Crate
@ = Crate and Target
O = Target


Player = Person or PersonInside

pattern = pattern0 or pattern1 or pattern2 or pattern3 or pattern4 or pattern5 or pattern6 or pattern7 or pattern8 or pattern9 or pattern10 or pattern11 or pattern12 or pattern13 or pattern14 or pattern15 or pattern16 or pattern17 or pattern18 or pattern19




=======
SOUNDS
=======

Crate MOVE 36772507

PersonInside create 74438701
PersonInside destroy 82957503

(no crate options sound)
sfx0 90720904 (33509502) (62262506)  (97691709)

(multiple crate options sound)
cancel 9703504 (5206902) (12711101) (48573106) ()

================
COLLISIONLAYERS
================

Background
pattern
Target
Person, Wall, SolidWall, Crate
PersonInside

TempPersonExistsAlready
TempCannotPushCrate




======
RULES
======



[ stationary player ][ pattern19 ] -> [ stationary player ][ pattern0 ]
[ stationary player ][ pattern18 ] -> [ stationary player ][ pattern19 ]
[ stationary player ][ pattern17 ] -> [ stationary player ][ pattern18 ]
[ stationary player ][ pattern16 ] -> [ stationary player ][ pattern17 ]
[ stationary player ][ pattern15 ] -> [ stationary player ][ pattern16 ]
[ stationary player ][ pattern14 ] -> [ stationary player ][ pattern15 ]
[ stationary player ][ pattern13 ] -> [ stationary player ][ pattern14 ]
[ stationary player ][ pattern12 ] -> [ stationary player ][ pattern13 ]
[ stationary player ][ pattern11 ] -> [ stationary player ][ pattern12 ]
[ stationary player ][ pattern10 ] -> [ stationary player ][ pattern11 ]
[ stationary player ][ pattern9 ] -> [ stationary player ][ pattern10 ]
[ stationary player ][ pattern8 ] -> [ stationary player ][ pattern9 ]
[ stationary player ][ pattern7 ] -> [ stationary player ][ pattern8 ]
[ stationary player ][ pattern6 ] -> [ stationary player ][ pattern7 ]
[ stationary player ][ pattern5 ] -> [ stationary player ][ pattern6 ]
[ stationary player ][ pattern4 ] -> [ stationary player ][ pattern5 ]
[ stationary player ][ pattern3 ] -> [ stationary player ][ pattern4 ]
[ stationary player ][ pattern2 ] -> [ stationary player ][ pattern3 ]
[ stationary player ][ pattern1 ] -> [ stationary player ][ pattern2 ]
[ stationary player ][ pattern0 ] -> [ stationary player ][ pattern1 ]





(push crates)
[ >  Person | Crate ] -> [  >  Person | > Crate  ]


(enter contiguous walls)

(random [PersonInside | Wall no PersonInside] -> [PersonInside | PersonInside] again
[> Person | Wall] -> [ | PersonInside] again 
slow growth - it would be possible to let players interrupt)

[> Person | Wall] -> [ | PersonInside pattern0] 
[PersonInside | Wall no PersonInside] -> [PersonInside | PersonInside pattern0] 



(if there are no possible crates to push, play sound)
	 [ moving PersonInside ] -> [  moving PersonInside TempCannotPushCrate ]
late [ Person no PersonInside ][ TempCannotPushCrate ] -> [ Person ][]
late [ TempCannotPushCrate ] -> [] sfx0


(exit walls)
[ > PersonInside | Crate ] -> [ PersonInside > Person | > Crate ]
[ moving PersonInside] -> [stationary PersonInside]


(stop crates against PersonInsides)
[> Crate | PersonInside] -> [Crate | PersonInside] 


(delete people that can't move)
late [Person PersonInside] -> [PersonInside]
(reconvert PersonInsides to walls)
late  [Person][PersonInside] -> [Person][Wall]


(ensure only one player, without using random)
late [Background] -> [Background TempPersonExistsAlready]
late left [TempPersonExistsAlready | TempPersonExistsAlready] -> [TempPersonExistsAlready | ]
+late up [TempPersonExistsAlready | TempPersonExistsAlready] -> [TempPersonExistsAlready | ]

late [Person][TempPersonExistsAlready no Person] -> [Person TempPersonExistsAlready][]
late [Person no TempPersonExistsAlready] -> cancel

late [TempPersonExistsAlready] -> []



late [ pattern no PersonInside ] -> []



(should it be that walls can walk around if not attached to anything?)
(should it be that multiple blocks next to walls spawn multiple main characters?)

==============
WINCONDITIONS
==============

All Target on Crate

=======
LEVELS
=======


(A level showing crates on targets?)

(%#%##%##%%
#%#.....##
%%#%##%.%#
###%#o..##
%#%#%%%.%%
#....%...%
%.#*.....#
%....%%.%%
##%%%#...%
%#%##%.p.%
#%#%#%###%)

%%%%%%%%%%%%%%%%
%%%%%%%####%%%%%
%%%%%%%#%%#%%%%%
%%###......##%%%
%%#%%%%%%%.%#%%%
%%#%%%%o...##%%%
%%#%%%%%%%.%%%%%
%%#%....%....#%%
%%##.#*......#%%
%%%%....%%.%%%%%
%%%%%#%%%...%%%%
%%%%%#%%%.p.%%%%
%%%%%###%###%%%%
%%%%%%%%%%%%%%%%
(
%%%#%#%##%##%%#%#%
#%%%#%#.....##%#%%
%%%%%%#%##%.%#%#%#
%#%%###%#o..##%%%%
%%%#%#%#%%%.%%#%%%
%%%%#....%...%%%#%
%%%%%.#*.....#%%%%
#%#%%....%%.%%%#%%
%%%%##%%%#...%%%%#
%%%#%#%##%.p.%#%%%
%#%%#%#%#%###%%#%%)


%%%%%%%%%%%%%
%%...#.....#%
%%.*.#..*..#%
%%...#.....#%
%%%.%%%.%%%#%
%.o..p...o.#%
%...%%%%...#%
%%%%%%%%%%%%%


%%%%%%%%%%%%%
%%%###%###%%%
%%%###%###%%%
%%%###%###%%%
%###########%
%.o.........%
%.*...*.%.o.%
%###p###%###%
%%%###%###%%%
%%%%%%%%%%%%%  (gap or solid wall as removing crate option)



.........
##.....##
%#.....#%
%#*.o..#%
%#*.o..#%
%#..o.*#%
%#.....#%
##..p..##
......... (crate as removing crate option) (have a double block to demonstrate that you cannot push 2 blocks?)

(
%%%%%%%%
%######%
%#oo.*#%
%#op..#%
%#....#%
%#*..*#%
%######%
%%%%%%%%

###########
#..*.p.*..#
#.........#
#.ooo@ooo.#
#.........#
#.*.*.*.*.#
###########
)

%%%%%%%%%%%
%#########%
%#......*#%
%#*..p...#%
%#*.....o#%
%#*....oo#%
%#..*.oo@#%
%#########%
%%%%%%%%%%% (free three w/ two, free from corner)


######
##..##
##o*##
######
#o.*.#
#o*.o#
#*..p#
######

















%%%%%%%%%%%%%
%###########%
%#.........#%
%#..*.*.*..#%
%#.#######.#%
%#..#####..#%
%##..ooo..##%
%%#...p...#%%
%%#########%%
%%%%%%%%%%%%%



%%%%%%%%%
%%#####%%
%%#...#%%
%##.p.##%
%#*...*#%
%#..o..#%
%#.o@o.#%
%#..o..#%
%#*...*#%
%#######%
%%%%%%%%%

(use two that you free to free three on same wall?)

%%%%%%%%%
%#######%
%#*.#.*#%
%#.o.o.#%
%#*o*o*#%
%#.o.o.#%
%#p...*#%
%#######%
%%%%%%%%%







%%%%%%%%%%%%%
%...........%
%....%p%....%
%....%.%....%
%.o.@.@.@.*.%
%%%%.%.%.%%%%
%%%%#####%%%%
%%%%%%%%%%%%% (some way to tempt you into placing 2 down?)





..###.
..#.#.
..#.##
###*o#
#.o..#
#*..p#
###### 
(I like that there's no way to get completely stuck the first time you enter the walls, so as to teach the mechanic) 



######
##po##
##*###
######
#....#
#*..*#
#.oo.#
###### (use block on wall as means of transport)









%%%%%%%%%
%...#...%
%.*.#.*.%
%...#...%
%..%%%..%
%..opo..%
%%%%%%%%%


#####
#**.#
#*.##
#p###
#o.##
#oo.#
##### 
(easy)

####
#@@#
#@o#
#p##
#..#
#.*#
####

####
#oo#
#.##
#..#
##*#
#p*#
####


%%%%%%%%%%%%%
%.....%.....%
%..*..#.....%
%.....%..*..%
%..*..#.....%
%....o%o....%
%%%#%%%%%#%%%
%....o%o....%
%.....#.....%
%..*..%.*.*.%
%.....#.....%
%.....%....p%
%%%%%%%%%%%%%

%%%%%%%%%%%%%
%...%...%...%
%.o.%.o.%.o.%
%...%...%...%
%%%%%%%%%%%%%
%...%...%...%
%.o.%.o.%.o.%
%...%...%...%
%%%%%%%%%%%%%
%...%...%...%
%.o.%.o.%.o.%
%...%...%..p%
%%%%%%%%%%%%%

%%%%%%%%%%%%%
%...%...%...%
%.o.#*o.#*o.%
%.*.%...%...%
%%#%%%#%%%#%%
%...%.*.%.*.%
%.o.%.o.%.o.%
%.*.%...%...%
%%#%%%#%%%#%%
%...%...%.*.%
%.o*#.o*#.o.%
%...%...%..p%
%%%%%%%%%%%%%

%%%%%%%%%%%%%
%...%...%...%
%...%.o.%.o.%
%.@.%...%...%
%%#%%%%%%%%%%
%...%...%...%
%.o.%.o.%.o.%
%...%...%...%
%%%%%%%%%%%%%
%...%...%...%
%.o.%.o.%.o.%
%...%...%..p%
%%%%%%%%%%%%%


%%%%%%%%%%%%%
%.o.%...%...%
%...%.o.#.o.%
%.*.%..*%.*.%
%%#%%#%#%%#%%
%...%...%...%
%.o.#*o.#*o.%
%.*.%...%...%
%%#%%%#%%%#%%
%...%...%...%
%.o*#.o*#.@.%
%...%...%..p%
%%%%%%%%%%%%% (this but more interesting. Can set one-way direction of start square, this isn't used)

%%%%%%%%%%
%..%..#..%
#..#..%..%
%%%%%#%#%%
%..#..%..%
%..%..#..%
%#%%#%%#%%
%..%..%..%
#..#..#..%
%%%%%%%%%%

(want: deducible end point
reusing squares
3-way junctions?)




(computer levels)

#####
#o*p#
#**o#
#*oo#
##### (hard)

######
##..##
##o*##
######
#o.*.#
#o*.o#
#*..p#
###### (good)

######
##@o##
##@*##
######
#**oo#
#o*o*#
#..*p#
###### (insanely hard)

.o...
##.*#
##.##
...##
..p##


