title Mortar
author Toomber
homepage www.puzzlescript.net

(again_interval 0.05)

realtime_interval 0.1

(
========
COLOURS
#001427 blue
#708d81 grey
#f4d58d yellow
#bf0603 red
#ba8712 #5d4409 browns
)

========
OBJECTS
========

Background
#ba8712 #5d4409
11111
01111
11101
11111
10111


Target
#bf0603
.....
.000.
.0.0.
.000.
.....

Wall
 #708d81 #001427
00010
11111
01000
11111
00010

Person
#bf0603 #bf0603 #bf0603 #bf0603
.000.
.111.
22222
.333.
.3.3.


Crate
#001427
00000
0...0
0...0
0...0
00000

PersonInside
#708d81 transparent #bf0603
00010
11111
01000
11111
00010

SolidWall
#4f635b (#708d81)

TempPersonExistsAlready
white

TempCannotPushCrate
white

pattern0
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
55434 
75346 
53257 
32355 
33443 
 
pattern1
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
45433 
76335 
74246 
53246 
33344 
 
pattern2
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
34432 
56423 
76335 
64346 
33344 
 
pattern3
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
34432 
46522 
67423 
65335 
44344 
 
pattern4
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
23443 
25531 
47532 
66434 
54445 
 
pattern5
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
33454 
13542 
35631 
56533 
55445 
 
pattern6
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
43455 
12553 
14642 
35642 
55544 
 
pattern7
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
54456 
32465 
12553 
24542 
55544 
 
pattern8
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
54456 
42366 
21465 
23553 
44544 
 
pattern9
#000000 #1F0000 #3F0000 #5F0000 #7F0000 #9F0000 #BF0000 #DF0000 #FF0000  
65445 
63357 
41356 
22454 
34443




=======
LEGEND
=======

. = Background
# = Wall
% = SolidWall
P = Person
Q = PersonInside
* = Crate
@ = Crate and Target
O = Target


Player = Person or PersonInside

pattern = pattern0 or pattern1 or pattern2 or pattern3 or pattern4 or pattern5 or pattern6 or pattern7 or pattern8 or pattern9



=======
SOUNDS
=======

Crate MOVE 36772507

PersonInside create 74438701
PersonInside destroy 82957503

(no crate options sound)
sfx0 90720904 (33509502) (62262506)  (97691709)

(multiple crate options sound)
cancel 9703504 (5206902) (12711101) (48573106) ()

================
COLLISIONLAYERS
================

Background
pattern
Target
Person, Wall, SolidWall, Crate
PersonInside

TempPersonExistsAlready
TempCannotPushCrate




======
RULES
======



[ stationary player ][ pattern9 ] -> [ stationary player ][ pattern0 ]
[ stationary player ][ pattern8 ] -> [ stationary player ][ pattern9 ]
[ stationary player ][ pattern7 ] -> [ stationary player ][ pattern8 ]
[ stationary player ][ pattern6 ] -> [ stationary player ][ pattern7 ]
[ stationary player ][ pattern5 ] -> [ stationary player ][ pattern6 ]
[ stationary player ][ pattern4 ] -> [ stationary player ][ pattern5 ]
[ stationary player ][ pattern3 ] -> [ stationary player ][ pattern4 ]
[ stationary player ][ pattern2 ] -> [ stationary player ][ pattern3 ]
[ stationary player ][ pattern1 ] -> [ stationary player ][ pattern2 ]
[ stationary player ][ pattern0 ] -> [ stationary player ][ pattern1 ]




(push crates)
[ >  Person | Crate ] -> [  >  Person | > Crate  ]


(enter contiguous walls)

(random [PersonInside | Wall no PersonInside] -> [PersonInside | PersonInside] again
[> Person | Wall] -> [ | PersonInside] again 
slow growth - it would be possible to let players interrupt)

[> Person | Wall] -> [ | PersonInside pattern0] 
[PersonInside | Wall no PersonInside] -> [PersonInside | PersonInside pattern0] 



(if there are no possible crates to push, play sound)
	 [ moving PersonInside ] -> [  moving PersonInside TempCannotPushCrate ]
late [ Person no PersonInside ][ TempCannotPushCrate ] -> [ Person ][]
late [ TempCannotPushCrate ] -> [] sfx0


(exit walls)
[ > PersonInside | Crate ] -> [ PersonInside > Person | > Crate ]
[ moving PersonInside] -> [stationary PersonInside]


(stop crates against PersonInsides)
[> Crate | PersonInside] -> [Crate | PersonInside] 


(delete people that can't move)
late [Person PersonInside] -> [PersonInside]
(reconvert PersonInsides to walls)
late  [Person][PersonInside] -> [Person][Wall]


(ensure only one player, without using random)
late [Background] -> [Background TempPersonExistsAlready]
late left [TempPersonExistsAlready | TempPersonExistsAlready] -> [TempPersonExistsAlready | ]
+late up [TempPersonExistsAlready | TempPersonExistsAlready] -> [TempPersonExistsAlready | ]

late [Person][TempPersonExistsAlready no Person] -> [Person TempPersonExistsAlready][]
late [Person no TempPersonExistsAlready] -> cancel

late [TempPersonExistsAlready] -> []



late [ pattern no PersonInside ] -> []



(should it be that walls can walk around if not attached to anything?)
(should it be that multiple blocks next to walls spawn multiple main characters?)

==============
WINCONDITIONS
==============

All Target on Crate

=======
LEVELS
=======


%#%##%##%%
#%#.....##
%%#%##%.%#
###%#o..##
%#%#%%%.%%
#....%...%
%.#*.....#
%....%%.%%
##%%%#...%
%#%##%.p.%
#%#%#%###%


%%%%%%%%%%%%%
%%...#.....#%
%%.*.#..*..#%
%%...#.....#%
%%%.%%%.%%%#%
%.o..p...o.#%
%...%%%%...#%
%%%%%%%%%%%%%


%%%%%%%%%%%%%
%%%###%###%%%
%%%###%###%%%
%%%###%###%%%
%###########%
%.o.........%
%.*...*.%.o.%
%###p###%###%
%%%###%###%%%
%%%%%%%%%%%%%  (gap or solid wall as removing crate option)






(crate as removing crate option)







%%%%%%%%%%%%%
%...........%
%....%p%....%
%....%.%....%
%.o.@.@.@.*.%
%%%%.%.%.%%%%
%%%%#####%%%%
%%%%%%%%%%%%% (some way to tempt you into placing 2 down?)





..###.
..#.#.
..#.##
###*o#
#.o..#
#*..p#
###### 
(I like that there's no way to get completely stuck the first time you enter the walls, so as to teach the mechanic) 


##########
#........#
#.....*..#
#.*####..#
#..#..#..#
#..#p.#..#
#.*####..#
#........#
#........#
##########

######
##po##
##*###
######
#....#
#*..*#
#.oo.#
###### (use block on wall as means of transport)

######
#....#
#*##o#
######
#.p..#
#*..*#
#.oo.#
######

######
#....#
#*##.#
####.#
#.p..#
#*..*#
#.ooo#
######








%%%%%%%%%
%...#...%
%.*.#.*.%
%...#...%
%..%%%..%
%..opo..%
%%%%%%%%%


#####
#**.#
#*.##
#p###
#o.##
#oo.#
##### 
(easy)

####
#@@#
#@o#
#p##
#..#
#.*#
####

####
#oo#
#.##
#..#
##*#
#p*#
####


%%%%%%%%%%%%%
%.....%.....%
%..*..#.....%
%.....%..*..%
%..*..#.....%
%....o%o....%
%%%#%%%%%#%%%
%....o%o....%
%.....#.....%
%..*..%.*.*.%
%.....#.....%
%.....%....p%
%%%%%%%%%%%%%



(computer levels)

#####
#o*p#
#**o#
#*oo#
##### (hard)

######
##..##
##o*##
######
#o.*.#
#o*.o#
#*..p#
###### (good)

######
##@o##
##@*##
######
#**oo#
#o*o*#
#..*p#
###### (insanely hard)

.o...
##.*#
##.##
...##
..p##


